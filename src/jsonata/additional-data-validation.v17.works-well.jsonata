    ( /*  is declaration and invoice total amount equal  25-07-2025 */
        $IO3000 := [];
        $A1140 := [];
        $A1460 := ["1", "0"];

        $errorMap := {
            "INVALID_IO_PARTNER": {
                "en": "IO Partner is not valid",
                "tr": "IO Partner geçerli değil"
            },
            "INVALID_IO_DIVISION3": {
                "en": "IO Division 3 is not valid",
                "tr": "IO Division 3 geçerli değil"
            },
            "INVALID_IO_REFERENCE": {
                "en": "IO Reference is not valid",
                "tr": "IO Reference geçerli değil"
            },
            "INVALID_OBJECT_NAME": {
                "en": "Object name is not valid",
                "tr": "Nesne adı geçerli değil"
            },
            "INVALID_OBJECT_ALIAS": {
                "en": "Object alias is not valid",
                "tr": "Nesne takma adı geçerli değil"
            },
            "INVALID_DECLARATION_TYPE": {
                "en": "Declaration type is not valid",
                "tr": "Beyanname türü geçerli değil"
            },
            "INVALID_USER_NAME": {
                "en": "Username is not valid",
                "tr": "Kullanıcı adı geçerli değil"
            },
            "INVALID_REFERENCE_NUMBER_OVERLAY": {
                "en": "Reference number overlay is not valid",
                "tr": "Referans numarası önblengi geçerli değil"
            },
            "INVALID_CUSTOMS_OFFICE": {
                "en": "Customs office is not valid",
                "tr": "Gümrük müdürlüğü geçerli değil"
            },
            "INVALID_REPRESENTATION_CODE": {
                "en": "Representation relationship code is not valid",
                "tr": "Temsil ilişkisi kodu geçerli değil"
            },
            "INVALID_CONTACT_PERSON_NAME": {
                "en": "Contact person name is not valid",
                "tr": "İletişim kişisi adı geçerli değil"
            },
            "INVALID_CONTACT_PERSON_PHONE": {
                "en": "Contact person phone number is not valid",
                "tr": "İletişim kişisi telefon numarası geçerli değil"
            },
            "INVALID_CONTACT_PERSON_POSITION": {
                "en": "Contact person position is not valid",
                "tr": "İletişim kişisi pozisyonu geçerli değil"
            },
            "INVALID_CONTACT_PERSON_EMAIL": {
                "en": "Contact person email is not valid",
                "tr": "İletişim kişisi e-posta adresi geçerli değil"
            },
            "INVALID_PROCEDURE_CODE": {
                "en": "Procedure code is not valid",
                "tr": "Prosedür kodu geçerli değil"
            },
            "INVALID_DEPARTURE_COUNTRY": {
                "en": "Departure country is not valid",
                "tr": "Kalkış ülkesi geçerli değil"
            },
            "INVALID_DESTINATION_STATE": {
                "en": "Destination federal state is not valid",
                "tr": "Hedef federal eyalet geçerli değil"
            },
            "INVALID_DESTINATION_COUNTRY": {
                "en": "Destination country is not valid",
                "tr": "Hedef ülke geçerli değil"
            },
            "INVALID_TRANSPORT_MEANS_IDENTITY": {
                "en": "Transport means arrival identity is not valid",
                "tr": "Taşıma aracı varış kimliği geçerli değil"
            },
            "INVALID_TRANSPORT_MEANS_NATIONALITY": {
                "en": "Transport means nationality code is not valid",
                "tr": "Taşıma aracı uyruk kodu geçerli değil"
            },
            "INVALID_PREVIOUS_REFERENCE_TYPE": {
                "en": "Previous administrative reference type is not valid",
                "tr": "Önceki idari referans türü geçerli değil"
            },
            "INVALID_PREVIOUS_REFERENCE_NUMBER": {
                "en": "Previous administrative reference number is not valid",
                "tr": "Önceki idari referans numarası geçerli değil"
            },
            "INVALID_DOCUMENT_DIVISION": {
                "en": "Document division is not valid",
                "tr": "Belge bölümü geçerli değil"
            },
            "INVALID_ADDRESS_TYPE": {
                "en": "Address type is not valid",
                "tr": "Adres türü geçerli değil"
            },
            "INVALID_ADDRESS_CODE": {
                "en": "Address code is not valid",
                "tr": "Adres kodu geçerli değil"
            },
            "INVALID_PARTICIPANT_EORI": {
                "en": "Participant EORI is not valid",
                "tr": "Katılımcı EORI geçerli değil"
            },
            "INVALID_PARTICIPANT_SUBSIDIARY_NUMBER": {
                "en": "Participant subsidiary number is not valid",
                "tr": "Katılımcı bağlı kuruluş numarası geçerli değil"
            },
            "INVALID_COMPANY_NAME": {
                "en": "Company name is not valid",
                "tr": "Şirket adı geçerli değil"
            },
            "INVALID_STREET_AND_NUMBER": {
                "en": "Street and number is not valid",
                "tr": "Sokak ve numara geçerli değil"
            },
            "INVALID_DISTRICT": {
                "en": "District is not valid",
                "tr": "İlçe geçerli değil"
            },
            "INVALID_COUNTRY_CODE": {
                "en": "Country code is not valid",
                "tr": "Ülke kodu geçerli değil"
            },
            "INVALID_POSTAL_CODE": {
                "en": "Postal code is not valid",
                "tr": "Posta kodu geçerli değil"
            },
            "INVALID_CITY": {
                "en": "City is not valid",
                "tr": "Şehir geçerli değil"
            },
            "MISSING_TRANSACTION": {
                "en": "Transaction is missing",
                "tr": "İşlem eksik"
            },
            "MISSING_IO_PARTNER": {
                "en": "IO Partner is missing",
                "tr": "IO Partner eksik"
            },
            "MISSING_IO_REFERENCE": {
                "en": "IO Reference is missing",
                "tr": "IO Reference eksik"
            },
            "MISSING_OBJECT_IDENTIFICATION": {
                "en": "Object identification is missing",
                "tr": "Nesne tanımlaması eksik"
            },
            "MISSING_OBJECT_NAME": {
                "en": "Object name is missing",
                "tr": "Nesne adı eksik"
            },
            "MISSING_USERNAME": {
                "en": "Username is missing",
                "tr": "Kullanıcı adı eksik"
            },
            "INVALID_DECLARANT_IS_CONSIGNEE": {
                "en": "Declarant is consignee flag is not valid",
                "tr": "Beyan eden alıcıdır işareti geçerli değil"
            },
            "INVALID_INPUT_TAX_DEDUCTION": {
                "en": "Input tax deduction flag is not valid",
                "tr": "KDV indirim işareti geçerli değil"
            },
            "INVALID_OBJECT_NAME_TOO_LONG": {
                "en": "Object name is too long, declaration index: {{1}}",
                "tr": "Nesne adı çok uzun, declaration index: {{1}}"
            },
            "INVALID_USERNAME": {
                "en": "Username is too long declaration index: {{1}}",
                "tr": "Kullanıcı adı çok uzun declaration index: {{1}}"
            },
            "NOT_EQUAL_INVOICE_TOTAL_AMOUNT": {
                "en": "Invoice total amount is not equal to the sum of invoice items. Total amount: {{1}}, Invoice items total amount: {{2}}",
                "tr": "Fatura toplam tutarı, fatura kalemlerinin toplamına eşit değil . Toplam tutar: {{1}}, Fatura kalemlerinin toplam tutarı: {{2}}"
            },
            "NOT_EQUAL_DECLARATION_TOTAL_VALUE": {
                "en": "Declaration total value is not equal to the sum of declaration items. Total value: {{1}}, Declaration items total value: {{2}}",
                "tr": "Beyanname toplam değeri, beyanname kalemlerinin toplamına eşit değil. Toplam değer: {{1}}, Beyanname kalemlerinin toplam değeri: {{2}}"
            },
            "INVALID_INVOICE_COMMODITY_CODE": {
                "en": "Invoice commodity code is not valid, invoice item index: {{1}}",
                "tr": "Fatura eşya kodu geçerli değil, fatura item index: {{1}}"
            },
            "INVALID_DECLARATION_COMMODITY_CODE": {
                "en": "Declaration commodity code is not valid, declaration index: {{1}}",
                "tr": "Beyanname eşya kodu geçerli değil, declaration index: {{1}}"
            },
            "INVALID_INVOICE_CURRENCY": {
                "en": "Invoice currency is not valid",
                "tr": "Fatura para birimi geçerli değil"
            },
            "INVALID_INVOICE_ITEM_COUNTRY_ORIGIN": {
                "en": "Invoice item country of origin is not valid, invoice item index: {{1}}",
                "tr": "Fatura kalemi menşe ülkesi geçerli değil, fatura item index: {{1}}"
            },
            "INVALID_DECLARATION_ITEM_COUNTRY_ORIGIN": {
                "en": "Declaration item country of origin is not valid, declaration index: {{1}}",
                "tr": "Beyanname kalemi menşe ülkesi geçerli değil, declaration index: {{1}}"
            },
            "NOT_EQUAL_DECLARATION_TOTAL_LINE_ITEM": {
                "en": "Declaration total line item is not equal to the number of declaration items. Total line item field value: {{1}}, Declaration items count: {{2}}",
                "tr": "Beyanname toplam satır kalemi, beyanname kalem sayısına eşit değil. Toplam satır kalem bilgisi: {{1}}, Beyanname kalem sayısı: {{2}}"
            },
            "NOT_EQUAL_DECLARATION_TOTAL_GROSS_WEIGHT": {
                "en": "Declaration total gross weight is not equal to the sum of declaration items. Total gross weight field value: {{1}}, Declaration items total gross weight: {{2}}",
                "tr": "Beyanname toplam brüt ağırlığı, beyanname kalemlerinin toplamına eşit değil. Toplam brüt ağırlık bilgisi: {{1}}, Beyanname kalemlerinin toplam brüt ağırlığı: {{2}}"
            },
            "NOT_EQUAL_DECLARATION_INVOICE_TOTAL_VALUE": {
                "en": "Declaration total value is not equal to the invoice total value. Declaration total value: {{1}}, Invoice total value: {{2}}",
                "tr": "Beyanname toplam değeri, fatura toplam değerine eşit değil. Beyanname toplam değeri: {{1}}, Fatura toplam değeri: {{2}}"
            }
        };

        $isDeclarationExist := $exists(tr_export_declaration);
        $isInvoiceExist := $exists(invoice);
        $isCmrExist := $exists(cmr);
        $isAtrExist := $exists(atr);

        $atoi := function($data) {$type($data) = "string" ? $round($number($replace($data, ",", ".")), 2) : $data};

        $isExist := function($data, $errorMessage) {$data ? null : $errorMessage};

        $max := function($data, $value, $errorMessage) { $data ? $length($data) <= $value ? null : $errorMessage : null };
        $min := function($data, $value, $errorMessage) { $data ?  $length($data) >= $value ? null : $errorMessage : null };
        $eq := function($data, $value, $errorMessage) { $length($data) = $value ? null : $errorMessage };
        $neq := function($data, $value, $errorMessage) { $length($data) != $value ? null : $errorMessage };

        $between := function($data, $min, $max, $errorMessage) { $type($data) = "string" ? $length($data) > $min and $length($data) <= $max ? null : $errorMessage : null };
        $betweene := function($data, $min, $max, $errorMessage) { $type($data) = "string" ? $length($data) >= $min and $length($data) <= $max ? null : $errorMessage : null };
        $nbetween := function($data, $min, $max, $errorMessage) { $type($data) = "string" ? $length($data) < $min or $length($data) > $max ? null : $errorMessage : null };

        $in := function($data, $values, $errorMessage) { $data in $values ? null : $errorMessage };
        $nin := function($data, $values, $errorMessage) { $data in $values ? $errorMessage : null };
        $like := function($data, $pattern, $errorMessage) { $match($data, $pattern) ? null : $errorMessage };
        $nlike := function($data, $pattern, $errorMessage) { $match($data, $pattern) ? $errorMessage : null };
        $startsWith := function($data, $prefix, $errorMessage) { $substring($data, 0, $length($prefix)) = $prefix ? null : $errorMessage };
        $endsWith := function($data, $suffix, $errorMessage) { $substring($data, $length($data) - $length($suffix)) = $suffix ? null : $errorMessage };
        $strContains := function($data, $substring, $errorMessage) { $contains($data, $substring) ? null : $errorMessage };
        $nStrContains := function($data, $substring, $errorMessage) { $contains($data, $substring) ? $errorMessage : null };
        $isNotNull := function($data, $errorMessage) { $data != null ? null : $errorMessage };
        $isNull := function($data, $errorMessage) { $data = null ? null : $errorMessage };
        $isNotEmpty := function($data, $errorMessage) { $data != null and $data != '' ? null : $errorMessage };
        $isEmpty := function($data, $errorMessage) { $data = null or $data = '' ? null : $errorMessage };

        $isBoolean := function($data, $errorMessage) { $data = true or $data = false ? null : $errorMessage };
        $isNumber := function($data, $errorMessage) { $type($data) = "number" ? null : $errorMessage };
        $isDateFormat := function($data, $errorMessage) {
            $match($data) ? null : $errorMessage
        };

        $formatErrorMessageObject := function($errorObject) {(
            $errorCode := $errorObject.code;
            $errorMessageObject := $lookup($errorMap, $errorCode);

            $message := $lookup($errorMessageObject, "en");

            $formattedMessage := $reduce($errorObject.args, function($acc, $arg, $index) {
                $replace($acc, "{{" & ($index + 1) & "}}", $string($arg))
            }, $message);

            $pathTmp := $reduce($errorObject.path, function($acc, $v, $i) {
                $isNumber($v, "") = null ?
                    ($i = 0 ? "[" & $v & "]" : $acc & "[" & $v & "]") : ($i = 0 ? $v : $acc & "." & $v)
            }, "");

            {
                "code": $errorCode,
                "message": $formattedMessage ? $formattedMessage : $message,
                "path": $pathTmp ? $pathTmp : "unknown"
            }
        )};

        $formatErrorMessageString := function($errorCode) {(
            $list := $split($errorCode, "&");
            $errorMessage := $lookup($errorMap, $list[0]);

            $message := $lookup($errorMessage, "en");

            {
                "code": $list[0],
                "message": $message ? $message : $list[0],
                "place": 1
            }
        )};

        $formatErrorMessage := function($errorCode) {
            $type($errorCode) = "object" ? $formatErrorMessageObject($errorCode) : $formatErrorMessageString($errorCode)
        };

        $customAppend := function($data) {(
            $data = $type($data) = "array" ? $data : $type($data) = "object" ? [$data] : [];

            $response := $reduce($data, $append)
        )};

        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*Additional ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $additionalWarningFirst := $filter([
            $between($.additional_data.transaction.ioPartner, 0, 100, {"code": "INVALID_IO_PARTNER", "path": ["additional_data", "transaction", "ioPartner"], "args": []}), /*IOPartner*/
            $between($.additional_data.transaction.ioDivision3, 0, 10, {"code": "INVALID_IO_DIVISION3", "path": ["additional_data", "transaction", "ioDivision3"], "args": []}), /*IODivision3*/
            $between($.additional_data.transaction.ioReference, 0, 35, {"code": "INVALID_IO_REFERENCE", "path": ["additional_data", "transaction", "ioReference"], "args": []}),

            $map($.additional_data.declaration, function($v, $i) {
                [
                    $between($v.objectIdentification.objectName, 0, 35, {"code": "INVALID_OBJECT_NAME", "path": ["additional_data", "declaration", $i, "objectIdentification", "objectName"], "args": [$i]}),
                    $between($v.objectIdentification.objectAlias, 0, 35, {"code": "INVALID_OBJECT_ALIAS", "path": ["additional_data", "declaration", $i, "objectIdentification", "objectAlias"], "args": [$i]}), /*ObjektAlias*/
                    $between($v.objectIdentification.declarationType, 0, 5, {"code": "INVALID_DECLARATION_TYPE", "path": ["additional_data", "declaration", $i, "objectIdentification", "declarationType"], "args": [$i]}), /*AnmeldungArt*/
                    $between($v.objectIdentification.username, 0, 70, {"code": "INVALID_USER_NAME", "path": ["additional_data", "declaration", $i, "objectIdentification", "username"], "args": [$i]}),
                    $between($v.objectIdentification.referenceNumberOverlay, 0, 35, {"code": "INVALID_REFERENCE_NUMBER_OVERLAY", "path": ["additional_data", "declaration", $i, "objectIdentification", "referenceNumberOverlay"], "args": [$i]}), /*BezugsnummerVorblendung*/

                    $between($v.headerData.addressedCustomsOffice, 0, 35, {"code": "INVALID_CUSTOMS_OFFICE", "path": ["additional_data", "declaration", $i, "headerData", "addressedCustomsOffice"], "args": [$i]}), /*AdressierteZollstelle*/
                    $between($v.headerData.representationRelationshipCode, 0, 1, {"code": "INVALID_REPRESENTATION_CODE", "path": ["additional_data", "declaration", $i, "headerData", "representationRelationshipCode"], "args": [$i]}), /*VertretungsverhaeltnisCode*/

                    $between($v.headerData.agentContact.contactPersonName, 0, 35, {"code": "INVALID_CONTACT_PERSON_NAME", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonName"], "args": [$i]}), /*NameAnmeldenderBearbeiter*/
                    $between($v.headerData.agentContact.contactPersonPhoneNumber, 0, 35, {"code": "INVALID_CONTACT_PERSON_PHONE", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonPhoneNumber"], "args": [$i]}), /*TelefonnummerAnmeldenderBearbeiter*/
                    $between($v.headerData.agentContact.contactPersonPosition, 0, 35, {"code": "INVALID_CONTACT_PERSON_POSITION", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonPosition"], "args": [$i]}), /*StellungAnmeldenderBearbeiter*/
                    $between($v.headerData.agentContact.contactPersonEmail, 0, 256, {"code": "INVALID_CONTACT_PERSON_EMAIL", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonEmail"], "args": [$i]}), /*EmailAdresseAnmeldenderBearbeiter*/

                    $between($v.headerData.procedureCodeRequested, 0, 2, {"code": "INVALID_PROCEDURE_CODE", "path": ["additional_data", "declaration", $i, "headerData", "procedureCodeRequested"], "args": [$i]}), /*FiskalvertretungKz*/
                    $between($v.headerData.departureCountry, 0, 2, {"code": "INVALID_DEPARTURE_COUNTRY", "path": ["additional_data", "declaration", $i, "headerData", "departureCountry"], "args": [$i]}), /*ZollrechtlicherStatus*/
                    $between($v.headerData.destinationFederalState, 0, 2, {"code": "INVALID_DESTINATION_STATE", "path": ["additional_data", "declaration", $i, "headerData", "destinationFederalState"], "args": [$i]}), /*Bestimmungsbundesland*/
                    $between($v.headerData.destinationCountry, 0, 2, {"code": "INVALID_DESTINATION_COUNTRY", "path": ["additional_data", "declaration", $i, "headerData", "destinationCountry"], "args": [$i]}), /*Bestimmungsland*/
                    $between($v.headerData.transportMeansArrivalIdentity, 0, 30, {"code": "INVALID_TRANSPORT_MEANS_IDENTITY", "path": ["additional_data", "declaration", $i, "headerData", "transportMeansArrivalIdentity"], "args": [$i]}), /*KennzeichenNameBefoerderungsmittelAnkunft*/
                    $between($v.headerData.transportMeansNationalityCode, 0, 2, {"code": "INVALID_TRANSPORT_MEANS_NATIONALITY", "path": ["additional_data", "declaration", $i, "headerData", "transportMeansNationalityCode"], "args": [$i]}), /*BefoerderungsmittelGrenzeStaatszugehoerigkeitCode*/
                    $between($v.headerData.previousAdministrativeReferenceType, 0, 6, {"code": "INVALID_PREVIOUS_REFERENCE_TYPE", "path": ["additional_data", "declaration", $i, "headerData", "previousAdministrativeReferenceType"], "args": [$i]}), /*VorpapierArtCode*/
                    $between($v.headerData.previousAdministrativeReferenceNumber, 0, 28, {"code": "INVALID_PREVIOUS_REFERENCE_NUMBER", "path": ["additional_data", "declaration", $i, "headerData", "previousAdministrativeReferenceNumber"], "args": [$i]}), /*VorpapierNr*/

                    /*Kostenart (dv1CostAllocation.costType)*/
                    /*Kosten (dv1CostAllocation.costs)*/
                    /*KostenWaehrung (dv1CostAllocation.currencyCode)*/

                    $between($v.headerData.base.documentDivision, 0, 1, {"code": "INVALID_DOCUMENT_DIVISION", "path": ["additional_data", "declaration", $i, "headerData", "base", "documentDivision"], "args": [$i]}),

                    $map($v.addresses, function($vv, $ii) {
                        [
                            $between($vv.addressType, 0, 10, {"code": "INVALID_ADDRESS_TYPE", "path": ["additional_data", "declaration", $i, "addresses", $ii, "addressType"], "args": [$i, $ii]}), /* "AdressTyp": $a.addressType, */
                            $between($vv.addressCode, 0, 10, {"code": "INVALID_ADDRESS_CODE", "path": ["additional_data", "declaration", $i, "addresses", $ii, "addressCode"], "args": [$i, $ii]}), /* "AdressCode": "1", */
                            $between($vv.participantEORI, 0, 17, {"code": "INVALID_PARTICIPANT_EORI", "path": ["additional_data", "declaration", $i, "addresses", $ii, "participantEORI"], "args": [$i, $ii]}), /* "TeilnehmerEORI": $a.participantEORI, */
                            $between($vv.participantSubsidiaryNumber, 0, 4, {"code": "INVALID_PARTICIPANT_SUBSIDIARY_NUMBER", "path": ["additional_data", "declaration", $i, "addresses", $ii, "participantSubsidiaryNumber"], "args": [$i, $ii]}),/* "TeilnehmerNLNR": $a.participantSubsidiaryNumber, */
                            $between($vv.companyName, 0, 120, {"code": "INVALID_COMPANY_NAME", "path": ["additional_data", "declaration", $i, "addresses", $ii, "companyName"], "args": [$i, $ii]}),/* "NameFirma": $a.companyName, */
                            $between($vv.streetAndNumber, 0, 35, {"code": "INVALID_STREET_AND_NUMBER", "path": ["additional_data", "declaration", $i, "addresses", $ii, "streetAndNumber"], "args": [$i, $ii]}), /* "StrasseHausNr": $a.streetAndNumber, */
                            $between($vv.district, 0, 35, {"code": "INVALID_DISTRICT", "path": ["additional_data", "declaration", $i, "addresses", $ii, "district"], "args": [$i, $ii]}), /* "Ortsteil": $a.district, */
                            $between($vv.countryCode, 0, 3, {"code": "INVALID_COUNTRY_CODE", "path": ["additional_data", "declaration", $i, "addresses", $ii, "countryCode"], "args": [$i, $ii]}), /* "LandCode": $a.countryCode, */
                            $between($vv.postalCode, 0, 9, {"code": "INVALID_POSTAL_CODE", "path": ["additional_data", "declaration", $i, "addresses", $ii, "postalCode"], "args": [$i, $ii]}), /* "PLZ-S": $a.postalCode, */
                            $between($vv.city, 0, 35, {"code": "INVALID_CITY", "path": ["additional_data", "declaration", $i, "addresses", $ii, "city"], "args": [$i, $ii]}) /* "Ort-S": $a.city */
                        ]
                    }) ~> $customAppend()
                ]
            }) ~> $customAppend()
        ], function($v) { $v != null });
        $additionalErrorsFirst := $filter([
            $isExist($.additional_data.transaction, {"code": "MISSING_TRANSACTION", "path": ["additional_data", "transaction"], "args": []}), /*Transaktion*/
            $isExist($.additional_data.transaction.ioPartner, {"code": "MISSING_IO_PARTNER", "path": ["additional_data", "transaction", "ioPartner"], "args": []}), /*IOPartner*/
            $isExist($.additional_data.transaction.ioReference, {"code": "MISSING_IO_REFERENCE", "path": ["additional_data", "transaction", "ioReference"], "args": []}), /*IOReferenz*/

            $map($.additional_data.declaration, function($v, $i) {
                [
                    $isExist($v.objectIdentification, {"code": "MISSING_OBJECT_IDENTIFICATION", "path": ["additional_data", "declaration", $i, "objectIdentification"], "args": [$i]}), /*ObjektIdentifizierung*/
                    $isExist($v.objectIdentification.objectName, {"code": "MISSING_OBJECT_NAME", "path": ["additional_data", "declaration", $i, "objectIdentification", "objectName"], "args": [$i]}), /*ObjektName*/
                    $isExist($v.objectIdentification.username, {"code": "MISSING_USERNAME", "path": ["additional_data", "declaration", $i, "objectIdentification", "username"], "args": [$i]}), /*Bearbeiter*/

                    $isBoolean($v.headerData.declarantIsConsignee, {"code": "INVALID_DECLARANT_IS_CONSIGNEE", "path": ["additional_data", "declaration", $i, "headerData", "declarantIsConsignee"], "args": [$i]}), /*AnmelderIstEmpfaenger*/
                    $isBoolean($v.headerData.inputTaxDeduction, {"code": "INVALID_INPUT_TAX_DEDUCTION", "path": ["additional_data", "declaration", $i, "headerData", "inputTaxDeduction"], "args": [$i]}) /*Vorsteuerabzug*/
                ]
            }) ~> $customAppend()
        ], function($v) { $v != null });
        $additionalErrors := $count($additionalErrorsFirst) ? $map($additionalErrorsFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];
        $additionalWarnings := $count($additionalWarningFirst) ? $map($additionalWarningFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];


        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*Invoice ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $isInvoiceTotalAmountEqual := function($invoice, $index) {(
            $itemTotalAmount := $reduce($invoice.items, function($acc, $v) {
                $acc + ($v.invoice_item_total_amount ? $v.invoice_item_total_amount : 0)
            }, 0);
            $invoiceTotalAmount := $invoice.invoice_total_amount ? $invoice.invoice_total_amount : 0;

            $itemTotalAmount = $invoiceTotalAmount ? null : {"code": "NOT_EQUAL_INVOICE_TOTAL_AMOUNT", "path": ["invoice", $index, "invoice_total_amount"], "args": [$invoiceTotalAmount, $itemTotalAmount]}
        )};
        $invoiceErrorsFirst := $isInvoiceExist and $type(invoice) = "object" ? 
            $filter([
                $isInvoiceTotalAmountEqual(invoice),
                $max($.invoice.invoice_currency, 3, {"code": "INVALID_INVOICE_CURRENCY", "path": ["invoice", "invoice_currency"], "args": []}),
                $map($.invoice.items, function($v, $i) {
                    [
                        $max($v.invoice_item_commodity_code, 11, {"code": "INVALID_INVOICE_COMMODITY_CODE", "path": ["invoice", "items", $i, "invoice_item_commodity_code"], "args": [$i]}),
                        $max($v.invoice_item_country_of_origin, 2, {"code": "INVALID_INVOICE_ITEM_COUNTRY_ORIGIN", "path": ["invoice", "items", $i, "invoice_item_country_of_origin"], "args": [$i]})
                    ]
                }) ~> $customAppend()
            ], function($v) { $v != null }) : 
        $isInvoiceExist and $type(invoice) = "array" ?
            $filter([
                $map(invoice, function($v, $i) {
                    [
                        $isInvoiceTotalAmountEqual($v, $i),
                        $max($v.invoice_currency, 3, {"code": "INVALID_INVOICE_CURRENCY", "path": ["invoice", $i, "invoice_currency"], "args": []}),
                        $map($v.items, function($vv, $ii) {
                            [
                                $max($vv.invoice_item_country_of_origin, 2, {"code": "INVALID_INVOICE_ITEM_COUNTRY_ORIGIN", "path": ["invoice", $i, "items", $ii, "invoice_item_country_of_origin"], "args": [$ii]}),
                                $max($vv.invoice_item_commodity_code, 11, {"code": "INVALID_INVOICE_COMMODITY_CODE", "path": ["invoice", $i, "items", $ii, "invoice_item_commodity_code"], "args": [$ii]})
                            ]
                        }) ~> $customAppend()
                    ]
                }) ~> $customAppend() 
            ], function($v) { $v != null })
        : [];
        $invoiceErrors := $count($invoiceErrorsFirst) ? $map($invoiceErrorsFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];
        $invoiceWarnings := [];

        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*tr_export_declaration ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $isEqualTotalValueDeclarationInvoice := function($declaration) {(
            $invoiceTotalValue := $isInvoiceExist and $type($.invoice) = "object" ? $.invoice.invoice_total_amount : $isInvoiceExist and $type($.invoice) = "array" ? $.invoice.invoice_total_amount ~> $sum() : 0;
            $declarationTotalValue := $isDeclarationExist and $declaration.total_value ? $atoi($declaration.total_value) : 0;

            $invoiceTotalValue = 0 or $declarationTotalValue = 0 or $round($invoiceTotalValue, 2) = $round($declarationTotalValue, 2) ? null : {"code": "NOT_EQUAL_DECLARATION_INVOICE_TOTAL_VALUE", "path": ["tr_export_declaration", "total_value"], "args": [$declarationTotalValue, $invoiceTotalValue] }
        )};
        $isEqualDeclarationTotalGrossWeight := function($declaration) {(
            $grossWeight := $declaration.total_weight ? $declaration.total_weight : $declaration.items[0].gross_weight;
            $itemTotalGrossWeight := $reduce($declaration.items, function($acc, $v) {
                $acc + ($v.gross_weight ? $v.gross_weight : 0)
            }, 0) ~> $round(2);

            $round($grossWeight, 2) = $itemTotalGrossWeight ? null : {"code": "NOT_EQUAL_DECLARATION_TOTAL_GROSS_WEIGHT", "path": ["tr_export_declaration", "total_weight"], "args": [$grossWeight, $itemTotalGrossWeight] }
        )};
        $isEqualDeclarationTotalItemLine := function($declaration) {(
            $totalLineItem := $declaration.total_line_item ~> $type() = "string" ? $atoi($declaration.total_line_item) : $declaration.total_line_item;
            $itemCount := $count($declaration.items);

            $totalLineItem = $itemCount ? null : {"code": "NOT_EQUAL_DECLARATION_TOTAL_LINE_ITEM", "path": ["tr_export_declaration", "total_line_item"], "args": [$totalLineItem, $itemCount] }
        )};
        $isDeclarationTotalValueEqual := function($declaration) {(
            $itemTotalValue := $reduce($declaration.items, function($acc, $v) {
                $acc + ($v.item_value ? $v.item_value : 0)
            }, 0);
            $declarationTotalValue := $declaration.total_value ? $atoi($declaration.total_value) : 0;

            $round($itemTotalValue, 2) = $declarationTotalValue ? null : { "code":"NOT_EQUAL_DECLARATION_TOTAL_VALUE", "path": ["tr_export_declaration", "total_value"], "args": [$declarationTotalValue, $itemTotalValue] }
        )};
        $declarationErrorsFirst := $isDeclarationExist ? $filter([
            $isEqualDeclarationTotalGrossWeight(tr_export_declaration),
            $isDeclarationTotalValueEqual(tr_export_declaration),
            $isEqualDeclarationTotalItemLine(tr_export_declaration),
            $isEqualTotalValueDeclarationInvoice(tr_export_declaration),
            $map($.tr_export_declaration.items, function($v, $i) {
                [
                    $max($v.commodity_code, 11, {"code": "INVALID_DECLARATION_COMMODITY_CODE", "path": ["tr_export_declaration", "items", $i, "commodity_code"], "args": [$i]}),
                    $max($v.origin, 2, {"code": "INVALID_DECLARATION_ITEM_COUNTRY_ORIGIN", "path": ["tr_export_declaration", "items", $i, "origin"], "args": [$i]})
                ]
            }) ~> $customAppend()
        ], function($v) { $v != null }) : [];
        $declarationErrors := $count($declarationErrorsFirst) ? $map($declarationErrorsFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];
        $declarationWarnings := [];


        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*Atr ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $atrErrors := $isAtrExist ? $filter([
        ], function($v) { $v 
        != null }) : [];


        {
            "additional_data": {
                "errors": $count($additionalErrors) = 1 ? [$additionalErrors] : $additionalErrors,
                "warnings": $count($additionalWarnings) = 1 ? [$additionalWarnings] : $additionalWarnings
            },
            "invoice": {
                "errors": $count($invoiceErrors) = 1 ? [$invoiceErrors] : $invoiceErrors,
                "warnings": $count($invoiceWarnings) = 1 ? [$invoiceWarnings] : $invoiceWarnings
            },
            "atr": {
                "errors": [],
                "warnings": []
            },
            "tr_export_declaration": {
                "errors": $count($declarationErrors) = 1 ? [$declarationErrors] : $declarationErrors,
                "warnings": $count($declarationWarnings) = 1 ? [$declarationWarnings] : $declarationWarnings
            }
        }
    )