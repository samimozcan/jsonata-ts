( /*  update error_0004 bugs   11-08-2025 */
        $IO3000 := [];
        $A1140 := [];
        $A1460 := ["1", "0"];

        $errorMap := {
            "INVALID_0001": {
                "en": "IO Partner is not valid",
                "tr": "IO Partner geçerli değil"
            },
            "INVALID_0002": {
                "en": "IO Division 3 is not valid",
                "tr": "IO Division 3 geçerli değil"
            },
            "INVALID_0003": {
                "en": "IO Reference is not valid",
                "tr": "IO Reference geçerli değil"
            },
            "INVALID_0004": {
                "en": "Object name is not valid",
                "tr": "Nesne adı geçerli değil"
            },
            "INVALID_0005": {
                "en": "Object alias is not valid",
                "tr": "Nesne takma adı geçerli değil"
            },
            "INVALID_0006": {
                "en": "Declaration type is not valid",
                "tr": "Beyanname türü geçerli değil"
            },
            "INVALID_0007": {
                "en": "Username is not valid",
                "tr": "Kullanıcı adı geçerli değil"
            },
            "INVALID_0008": {
                "en": "Reference number overlay is not valid",
                "tr": "Referans numarası önblengi geçerli değil"
            },
            "INVALID_0009": {
                "en": "Customs office is not valid",
                "tr": "Gümrük müdürlüğü geçerli değil"
            },
            "INVALID_0010": {
                "en": "Representation relationship code is not valid",
                "tr": "Temsil ilişkisi kodu geçerli değil"
            },
            "INVALID_0011": {
                "en": "Contact person name is not valid",
                "tr": "İletişim kişisi adı geçerli değil"
            },
            "INVALID_0012": {
                "en": "Contact person phone number is not valid",
                "tr": "İletişim kişisi telefon numarası geçerli değil"
            },
            "INVALID_0013": {
                "en": "Contact person position is not valid",
                "tr": "İletişim kişisi pozisyonu geçerli değil"
            },
            "INVALID_0014": {
                "en": "Contact person email is not valid",
                "tr": "İletişim kişisi e-posta adresi geçerli değil"
            },
            "INVALID_0015": {
                "en": "Procedure code is not valid",
                "tr": "Prosedür kodu geçerli değil"
            },
            "INVALID_0016": {
                "en": "Departure country is not valid",
                "tr": "Kalkış ülkesi geçerli değil"
            },
            "INVALID_0017": {
                "en": "Destination federal state is not valid",
                "tr": "Hedef federal eyalet geçerli değil"
            },
            "INVALID_0018": {
                "en": "Destination country is not valid",
                "tr": "Hedef ülke geçerli değil"
            },
            "INVALID_0019": {
                "en": "Transport means arrival identity is not valid",
                "tr": "Taşıma aracı varış kimliği geçerli değil"
            },
            "INVALID_0020": {
                "en": "Transport means nationality code is not valid",
                "tr": "Taşıma aracı uyruk kodu geçerli değil"
            },
            "INVALID_0021": {
                "en": "Previous administrative reference type is not valid",
                "tr": "Önceki idari referans türü geçerli değil"
            },
            "INVALID_0022": {
                "en": "Previous administrative reference number is not valid",
                "tr": "Önceki idari referans numarası geçerli değil"
            },
            "INVALID_0023": {
                "en": "Document division is not valid",
                "tr": "Belge bölümü geçerli değil"
            },
            "INVALID_0024": {
                "en": "Address type is not valid",
                "tr": "Adres türü geçerli değil"
            },
            "INVALID_0025": {
                "en": "Address code is not valid",
                "tr": "Adres kodu geçerli değil"
            },
            "INVALID_0026": {
                "en": "Participant EORI is not valid",
                "tr": "Katılımcı EORI geçerli değil"
            },
            "INVALID_0027": {
                "en": "Participant subsidiary number is not valid",
                "tr": "Katılımcı bağlı kuruluş numarası geçerli değil"
            },
            "INVALID_0028": {
                "en": "Company name is not valid",
                "tr": "Şirket adı geçerli değil"
            },
            "INVALID_0029": {
                "en": "Street and number is not valid",
                "tr": "Sokak ve numara geçerli değil"
            },
            "INVALID_0030": {
                "en": "District is not valid",
                "tr": "İlçe geçerli değil"
            },
            "INVALID_0031": {
                "en": "Country code is not valid",
                "tr": "Ülke kodu geçerli değil"
            },
            "INVALID_0032": {
                "en": "Postal code is not valid",
                "tr": "Posta kodu geçerli değil"
            },
            "INVALID_0033": {
                "en": "City is not valid",
                "tr": "Şehir geçerli değil"
            },
            "MISSING_0001": {
                "en": "Transaction is missing",
                "tr": "İşlem eksik"
            },
            "MISSING_0002": {
                "en": "IO Partner is missing",
                "tr": "IO Partner eksik"
            },
            "MISSING_0003": {
                "en": "IO Reference is missing",
                "tr": "IO Reference eksik"
            },
            "MISSING_0004": {
                "en": "Object identification is missing",
                "tr": "Nesne tanımlaması eksik"
            },
            "MISSING_0005": {
                "en": "Object name is missing",
                "tr": "Nesne adı eksik"
            },
            "MISSING_0006": {
                "en": "Username is missing",
                "tr": "Kullanıcı adı eksik"
            },
            "MISSING_0007": {
                "en": "Invoice item commodity code is missing",
                "tr": "Fatura kalemi eşya kodu eksik"
            },
            "INVALID_0034": {
                "en": "Declarant is consignee flag is not valid",
                "tr": "Beyan eden alıcıdır işareti geçerli değil"
            },
            "INVALID_0035": {
                "en": "Input tax deduction flag is not valid",
                "tr": "KDV indirim işareti geçerli değil"
            },
            "INVALID_0036": {
                "en": "Object name is too long, declaration index: {{1}}",
                "tr": "Nesne adı çok uzun, declaration index: {{1}}"
            },
            "INVALID_0037": {
                "en": "Username is too long declaration index: {{1}}",
                "tr": "Kullanıcı adı çok uzun declaration index: {{1}}"
            },
            "ERROR_0001": {
                "en": "Invoice total amount is not equal to the sum of invoice items. Total amount: {{1}}, Invoice items total amount: {{2}}",
                "tr": "Fatura toplam tutarı, fatura kalemlerinin toplamına eşit değil . Toplam tutar: {{1}}, Fatura kalemlerinin toplam tutarı: {{2}}"
            },
            "ERROR_0002": {
                "en": "Declaration total value is not equal to the sum of declaration items. Total value: {{1}}, Declaration items total value: {{2}}",
                "tr": "Beyanname toplam değeri, beyanname kalemlerinin toplamına eşit değil. Toplam değer: {{1}}, Beyanname kalemlerinin toplam değeri: {{2}}"
            },
            "INVALID_0038": {
                "en": "Invoice commodity code is not valid, invoice item index: {{1}}",
                "tr": "Fatura eşya kodu geçerli değil, fatura item index: {{1}}"
            },
            "INVALID_0039": {
                "en": "Declaration commodity code is not valid, declaration index: {{1}}",
                "tr": "Beyanname eşya kodu geçerli değil, declaration index: {{1}}"
            },
            "INVALID_0040": {
                "en": "Invoice currency is not valid",
                "tr": "Fatura para birimi geçerli değil"
            },
            "INVALID_0041": {
                "en": "Invoice item country of origin is not valid, invoice item index: {{1}}",
                "tr": "Fatura kalemi menşe ülkesi geçerli değil, fatura item index: {{1}}"
            },
            "INVALID_0042": {
                "en": "Declaration item country of origin is not valid, declaration index: {{1}}",
                "tr": "Beyanname kalemi menşe ülkesi geçerli değil, declaration index: {{1}}"
            },
            "ERROR_0003": {
                "en": "Declaration total line item is not equal to the number of declaration items. Total line item field value: {{1}}, Declaration items count: {{2}}",
                "tr": "Beyanname toplam satır kalemi, beyanname kalem sayısına eşit değil. Toplam satır kalem bilgisi: {{1}}, Beyanname kalem sayısı: {{2}}"
            },
            "ERROR_0004": {
                "en": "Declaration total gross weight is not equal to the sum of declaration items. Total gross weight field value: {{1}}, Declaration items total gross weight: {{2}}",
                "tr": "Beyanname toplam brüt ağırlığı, beyanname kalemlerinin toplamına eşit değil. Toplam brüt ağırlık bilgisi: {{1}}, Beyanname kalemlerinin toplam brüt ağırlığı: {{2}}"
            },
            "ERROR_0005": {
                "en": "Declaration total value is not equal to the invoice total value. Declaration total value: {{1}}, Invoice total value: {{2}}",
                "tr": "Beyanname toplam değeri, fatura toplam değerine eşit değil. Beyanname toplam değeri: {{1}}, Fatura toplam değeri: {{2}}"
            },
            "ERROR_0006": {
                "en": "Invoice total quantity is not equal to the sum of invoice items. Total quantity: {{1}}, Invoice items total quantity: {{2}}",
                "tr": "Fatura toplam miktarı, fatura kalemlerinin toplamına eşit değil. Toplam miktar: {{1}}, Fatura kalemlerinin toplam miktarı: {{2}}"
            }
        };

        $isDeclarationExist := $exists(tr_export_declaration) and $type(tr_export_declaration) = "object";
        $isInvoiceExist := $exists(invoice) and $type(invoice) = "array" and $count(invoice) > 0;
        $isCmrExist := $exists(cmr) and $type(cmr) = "object";
        $isAtrExist := $exists(atr) and $type(atr) = "object";
        $isCombineItemsExist := $exists(combine_items) and $type(combine_items) = "object";


        $atoi := function($data) {$type($data) = "string" ? $round($number($replace($data, ",", ".")), 2) : $data};

        $isExist := function($data, $errorMessage) {$data ? null : $errorMessage};

        $max := function($data, $value, $errorMessage) { $data ? $length($data) <= $value ? null : $errorMessage : null };
        $min := function($data, $value, $errorMessage) { $data ?  $length($data) >= $value ? null : $errorMessage : null };
        $eq := function($data, $value, $errorMessage) { $length($data) = $value ? null : $errorMessage };
        $neq := function($data, $value, $errorMessage) { $length($data) != $value ? null : $errorMessage };

        $between := function($data, $min, $max, $errorMessage) { $type($data) = "string" ? $length($data) > $min and $length($data) <= $max ? null : $errorMessage : null };
        $betweene := function($data, $min, $max, $errorMessage) { $type($data) = "string" ? $length($data) >= $min and $length($data) <= $max ? null : $errorMessage : null };
        $nbetween := function($data, $min, $max, $errorMessage) { $type($data) = "string" ? $length($data) < $min or $length($data) > $max ? null : $errorMessage : null };

        $in := function($data, $values, $errorMessage) { $data in $values ? null : $errorMessage };
        $nin := function($data, $values, $errorMessage) { $data in $values ? $errorMessage : null };
        $like := function($data, $pattern, $errorMessage) { $match($data, $pattern) ? null : $errorMessage };
        $nlike := function($data, $pattern, $errorMessage) { $match($data, $pattern) ? $errorMessage : null };
        $startsWith := function($data, $prefix, $errorMessage) { $substring($data, 0, $length($prefix)) = $prefix ? null : $errorMessage };
        $endsWith := function($data, $suffix, $errorMessage) { $substring($data, $length($data) - $length($suffix)) = $suffix ? null : $errorMessage };
        $strContains := function($data, $substring, $errorMessage) { $contains($data, $substring) ? null : $errorMessage };
        $nStrContains := function($data, $substring, $errorMessage) { $contains($data, $substring) ? $errorMessage : null };
        $isNotNull := function($data, $errorMessage) { $data != null ? null : $errorMessage };
        $isNull := function($data, $errorMessage) { $data = null ? null : $errorMessage };
        $isNotEmpty := function($data, $errorMessage) { $data != null and $data != '' ? null : $errorMessage };
        $isEmpty := function($data, $errorMessage) { $data = null or $data = '' ? null : $errorMessage };

        $isBoolean := function($data, $errorMessage) { $data = true or $data = false ? null : $errorMessage };
        $isNumber := function($data, $errorMessage) { $type($data) = "number" ? null : $errorMessage };
        $isDateFormat := function($data, $errorMessage) {
            $match($data) ? null : $errorMessage
        };

        $formatErrorMessageObject := function($errorObject) {(
            $errorCode := $errorObject.code;
            $errorMessageObject := $lookup($errorMap, $errorCode);

            $message := $lookup($errorMessageObject, "en");

            $formattedMessage := $reduce($errorObject.args, function($acc, $arg, $index) {
                $replace($acc, "{{" & ($index + 1) & "}}", $string($arg))
            }, $message);

            $pathTmp := $reduce($errorObject.path, function($acc, $v, $i) {
                $isNumber($v, "") = null ?
                    ($i = 0 ? "[" & $v & "]" : $acc & "[" & $v & "]") : ($i = 0 ? $v : $acc & "." & $v)
            }, "");

            {
                "code": $errorCode,
                "message": $formattedMessage ? $formattedMessage : $message,
                "path": $pathTmp ? $pathTmp : "unknown"
            }
        )};

        $formatErrorMessageString := function($errorCode) {(
            $list := $split($errorCode, "&");
            $errorMessage := $lookup($errorMap, $list[0]);

            $message := $lookup($errorMessage, "en");

            {
                "code": $list[0],
                "message": $message ? $message : $list[0],
                "place": 1
            }
        )};

        $formatErrorMessage := function($errorCode) {
            $type($errorCode) = "object" ? $formatErrorMessageObject($errorCode) : $formatErrorMessageString($errorCode)
        };

        $customAppend := function($data) {(
            $data = $type($data) = "array" ? $data : $type($data) = "object" ? [$data] : [];

            $response := $reduce($data, $append)
        )};

        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*Additional ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $additionalWarningFirst := $filter([
            $between($.additional_data.transaction.ioPartner, 0, 100, {"code": "INVALID_0001", "path": ["additional_data", "transaction", "ioPartner"], "args": []}), /*IOPartner*/
            $between($.additional_data.transaction.ioDivision3, 0, 10, {"code": "INVALID_0002", "path": ["additional_data", "transaction", "ioDivision3"], "args": []}), /*IODivision3*/
            $between($.additional_data.transaction.ioReference, 0, 35, {"code": "INVALID_0003", "path": ["additional_data", "transaction", "ioReference"], "args": []}),

            $map($.additional_data.declaration, function($v, $i) {
                [
                    $between($v.objectIdentification.objectName, 0, 35, {"code": "INVALID_0004", "path": ["additional_data", "declaration", $i, "objectIdentification", "objectName"], "args": [$i]}),
                    $between($v.objectIdentification.objectAlias, 0, 35, {"code": "INVALID_0005", "path": ["additional_data", "declaration", $i, "objectIdentification", "objectAlias"], "args": [$i]}), /*ObjektAlias*/
                    $between($v.objectIdentification.declarationType, 0, 5, {"code": "INVALID_0006", "path": ["additional_data", "declaration", $i, "objectIdentification", "declarationType"], "args": [$i]}), /*AnmeldungArt*/
                    $between($v.objectIdentification.username, 0, 70, {"code": "INVALID_0007", "path": ["additional_data", "declaration", $i, "objectIdentification", "username"], "args": [$i]}),
                    $between($v.objectIdentification.referenceNumberOverlay, 0, 35, {"code": "INVALID_0008", "path": ["additional_data", "declaration", $i, "objectIdentification", "referenceNumberOverlay"], "args": [$i]}), /*BezugsnummerVorblendung*/

                    $between($v.headerData.addressedCustomsOffice, 0, 35, {"code": "INVALID_0009", "path": ["additional_data", "declaration", $i, "headerData", "addressedCustomsOffice"], "args": [$i]}), /*AdressierteZollstelle*/
                    $between($v.headerData.representationRelationshipCode, 0, 1, {"code": "INVALID_0010", "path": ["additional_data", "declaration", $i, "headerData", "representationRelationshipCode"], "args": [$i]}), /*VertretungsverhaeltnisCode*/

                    $between($v.headerData.agentContact.contactPersonName, 0, 35, {"code": "INVALID_0011", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonName"], "args": [$i]}), /*NameAnmeldenderBearbeiter*/
                    $between($v.headerData.agentContact.contactPersonPhoneNumber, 0, 35, {"code": "INVALID_0012", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonPhoneNumber"], "args": [$i]}), /*TelefonnummerAnmeldenderBearbeiter*/
                    $between($v.headerData.agentContact.contactPersonPosition, 0, 35, {"code": "INVALID_0013", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonPosition"], "args": [$i]}), /*StellungAnmeldenderBearbeiter*/
                    $between($v.headerData.agentContact.contactPersonEmail, 0, 256, {"code": "INVALID_0014", "path": ["additional_data", "declaration", $i, "headerData", "agentContact", "contactPersonEmail"], "args": [$i]}), /*EmailAdresseAnmeldenderBearbeiter*/

                    $between($v.headerData.procedureCodeRequested, 0, 2, {"code": "INVALID_0015", "path": ["additional_data", "declaration", $i, "headerData", "procedureCodeRequested"], "args": [$i]}), /*FiskalvertretungKz*/
                    $between($v.headerData.departureCountry, 0, 2, {"code": "INVALID_0016", "path": ["additional_data", "declaration", $i, "headerData", "departureCountry"], "args": [$i]}), /*ZollrechtlicherStatus*/
                    $between($v.headerData.destinationFederalState, 0, 2, {"code": "INVALID_0017", "path": ["additional_data", "declaration", $i, "headerData", "destinationFederalState"], "args": [$i]}), /*Bestimmungsbundesland*/
                    $between($v.headerData.destinationCountry, 0, 2, {"code": "INVALID_0018", "path": ["additional_data", "declaration", $i, "headerData", "destinationCountry"], "args": [$i]}), /*Bestimmungsland*/
                    $between($v.headerData.transportMeansArrivalIdentity, 0, 30, {"code": "INVALID_0019", "path": ["additional_data", "declaration", $i, "headerData", "transportMeansArrivalIdentity"], "args": [$i]}), /*KennzeichenNameBefoerderungsmittelAnkunft*/
                    $between($v.headerData.transportMeansNationalityCode, 0, 2, {"code": "INVALID_0020", "path": ["additional_data", "declaration", $i, "headerData", "transportMeansNationalityCode"], "args": [$i]}), /*BefoerderungsmittelGrenzeStaatszugehoerigkeitCode*/
                    $between($v.headerData.previousAdministrativeReferenceType, 0, 6, {"code": "INVALID_0021", "path": ["additional_data", "declaration", $i, "headerData", "previousAdministrativeReferenceType"], "args": [$i]}), /*VorpapierArtCode*/
                    $between($v.headerData.previousAdministrativeReferenceNumber, 0, 28, {"code": "INVALID_0022", "path": ["additional_data", "declaration", $i, "headerData", "previousAdministrativeReferenceNumber"], "args": [$i]}), /*VorpapierNr*/

                    /*Kostenart (dv1CostAllocation.costType)*/
                    /*Kosten (dv1CostAllocation.costs)*/
                    /*KostenWaehrung (dv1CostAllocation.currencyCode)*/

                    $between($v.headerData.base.documentDivision, 0, 1, {"code": "INVALID_0023", "path": ["additional_data", "declaration", $i, "headerData", "base", "documentDivision"], "args": [$i]}),

                    $map($v.addresses, function($vv, $ii) {
                        [
                            $between($vv.addressType, 0, 10, {"code": "INVALID_0024", "path": ["additional_data", "declaration", $i, "addresses", $ii, "addressType"], "args": [$i, $ii]}), /* "AdressTyp": $a.addressType, */
                            $between($vv.addressCode, 0, 10, {"code": "INVALID_0025", "path": ["additional_data", "declaration", $i, "addresses", $ii, "addressCode"], "args": [$i, $ii]}), /* "AdressCode": "1", */
                            $between($vv.participantEORI, 0, 17, {"code": "INVALID_0026", "path": ["additional_data", "declaration", $i, "addresses", $ii, "participantEORI"], "args": [$i, $ii]}), /* "TeilnehmerEORI": $a.participantEORI, */
                            $between($vv.participantSubsidiaryNumber, 0, 4, {"code": "INVALID_0027", "path": ["additional_data", "declaration", $i, "addresses", $ii, "participantSubsidiaryNumber"], "args": [$i, $ii]}),/* "TeilnehmerNLNR": $a.participantSubsidiaryNumber, */
                            $between($vv.companyName, 0, 120, {"code": "INVALID_0028", "path": ["additional_data", "declaration", $i, "addresses", $ii, "companyName"], "args": [$i, $ii]}),/* "NameFirma": $a.companyName, */
                            $between($vv.streetAndNumber, 0, 35, {"code": "INVALID_0029", "path": ["additional_data", "declaration", $i, "addresses", $ii, "streetAndNumber"], "args": [$i, $ii]}), /* "StrasseHausNr": $a.streetAndNumber, */
                            $between($vv.district, 0, 35, {"code": "INVALID_0030", "path": ["additional_data", "declaration", $i, "addresses", $ii, "district"], "args": [$i, $ii]}), /* "Ortsteil": $a.district, */
                            $between($vv.countryCode, 0, 3, {"code": "INVALID_0031", "path": ["additional_data", "declaration", $i, "addresses", $ii, "countryCode"], "args": [$i, $ii]}), /* "LandCode": $a.countryCode, */
                            $between($vv.postalCode, 0, 9, {"code": "INVALID_0032", "path": ["additional_data", "declaration", $i, "addresses", $ii, "postalCode"], "args": [$i, $ii]}), /* "PLZ-S": $a.postalCode, */
                            $between($vv.city, 0, 35, {"code": "INVALID_0033", "path": ["additional_data", "declaration", $i, "addresses", $ii, "city"], "args": [$i, $ii]}) /* "Ort-S": $a.city */
                        ]
                    }) ~> $customAppend()
                ]
            }) ~> $customAppend()
        ], function($v) { $v != null });
        $additionalErrorsFirst := $filter([
            $isExist($.additional_data.transaction.ioPartner, {"code": "MISSING_0002", "path": ["additional_data", "transaction", "ioPartner"], "args": []}), /*IOPartner*/
            $isExist($.additional_data.transaction.ioReference, {"code": "MISSING_0003", "path": ["additional_data", "transaction", "ioReference"], "args": []}), /*IOReferenz*/

            $map($.additional_data.declaration, function($v, $i) {
                [
                    $isExist($v.objectIdentification.objectName, {"code": "MISSING_0005", "path": ["additional_data", "declaration", $i, "objectIdentification", "objectName"], "args": [$i]}), /*ObjektName*/
                    $isExist($v.objectIdentification.username, {"code": "MISSING_0006", "path": ["additional_data", "declaration", $i, "objectIdentification", "username"], "args": [$i]}), /*Bearbeiter*/

                    $isBoolean($v.headerData.declarantIsConsignee, {"code": "INVALID_0034", "path": ["additional_data", "declaration", $i, "headerData", "declarantIsConsignee"], "args": [$i]}), /*AnmelderIstEmpfaenger*/
                    $isBoolean($v.headerData.inputTaxDeduction, {"code": "INVALID_0035", "path": ["additional_data", "declaration", $i, "headerData", "inputTaxDeduction"], "args": [$i]}) /*Vorsteuerabzug*/
                ]
            }) ~> $customAppend()
        ], function($v) { $v != null });
        $additionalErrors := $count($additionalErrorsFirst) ? $map($additionalErrorsFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];
        $additionalWarnings := $count($additionalWarningFirst) ? $map($additionalWarningFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];


        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*Invoice ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $isInvoiceTotalAmountEqual := function($invoice, $index) {(
            $itemTotalAmount := $reduce($invoice.items, function($acc, $v) {
                $acc + ($v.invoice_item_total_amount ? $v.invoice_item_total_amount : 0)
            }, 0) ~> $round(2);
            $invoiceTotalAmount := $invoice.invoice_total_amount ? $invoice.invoice_total_amount ~> $round(2) : 0;

            $itemTotalAmount = $invoiceTotalAmount ? null : {"code": "ERROR_0001", "path": ["invoice", $index, "invoice_total_amount"], "args": [$invoiceTotalAmount, $itemTotalAmount]}
        )};
        $isInvoiceTotalQuantityEqual := function($invoice, $index) {(
            $itemTotalQuantity := $reduce($invoice.items, function($acc, $v) {
                $acc + ($v.invoice_item_quantity ? $v.invoice_item_quantity : 0)
            }, 0) ~> $round(2);
            $invoiceTotalQuantity := $invoice.invoice_total_quantity ? $invoice.invoice_total_quantity ~> $round(2) : 0;

            $itemTotalQuantity = $invoiceTotalQuantity ? null : {"code": "ERROR_0006", "path": ["invoice", $index, "invoice_total_quantity"], "args": [$invoiceTotalQuantity, $itemTotalQuantity]}
        )};
        $invoiceErrorsFirst := $isInvoiceExist and $type(invoice) = "array" ?
            $filter([
                $map(invoice, function($v, $i) {
                    [
                        $isInvoiceTotalAmountEqual($v, $i),
                        $isInvoiceTotalQuantityEqual($v, $i),
                        $max($v.invoice_currency, 3, {"code": "INVALID_0040", "path": ["invoice", $i, "invoice_currency"], "args": []}),
                        $map($v.items, function($vv, $ii) {
                            [
                                /* Country of Origin Validations*/
                                $max($vv.invoice_item_country_of_origin, 2, {"code": "INVALID_0041", "path": ["invoice", $i, "items", $ii, "invoice_item_country_of_origin"], "args": [$ii]}),

                                /* Commodity Code Validations*/
                                $isExist($vv.invoice_item_commodity_code, {"code": "MISSING_0007", "path": ["invoice", $i, "items", $ii, "invoice_item_commodity_code"], "args": [$ii]}),
                                $max($vv.invoice_item_commodity_code, 11, {"code": "INVALID_0038", "path": ["invoice", $i, "items", $ii, "invoice_item_commodity_code"], "args": [$ii]})

                            ]
                        }) ~> $customAppend()
                    ]
                }) ~> $customAppend() 
            ], function($v) { $v != null })
        : [];
        $invoiceErrors := $count($invoiceErrorsFirst) ? $map($invoiceErrorsFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];
        $invoiceWarnings := [];

        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*tr_export_declaration ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $isEqualTotalValueDeclarationInvoice := function($declaration) {(
            $invoiceTotalValue := $isInvoiceExist and $type($.invoice) = "object" ? $.invoice.invoice_total_amount : $isInvoiceExist and $type($.invoice) = "array" ? $.invoice.invoice_total_amount ~> $sum() : 0;
            $declarationTotalValue := $isDeclarationExist and $declaration.total_value ? $atoi($declaration.total_value) : 0;

            $invoiceTotalValue = 0 or $declarationTotalValue = 0 or $round($invoiceTotalValue, 2) = $round($declarationTotalValue, 2) ? null : {"code": "ERROR_0005", "path": ["tr_export_declaration", "total_value"], "args": [$declarationTotalValue, $invoiceTotalValue] }
        )};
        $isEqualDeclarationTotalGrossWeight := function($declaration) {(
            $grossWeight := $declaration.total_weight ? $declaration.total_weight : 0;
            $itemTotalGrossWeight := $reduce($declaration.items, function($acc, $v) {
                $acc + ($v.gross_weight ? $v.gross_weight : 0)
            }, 0) ~> $round(2);

            ($round($grossWeight, 2) = $itemTotalGrossWeight or $grossWeight = 0) ? null : {"code": "ERROR_0004", "path": ["tr_export_declaration", "total_weight"], "args": [$grossWeight, $itemTotalGrossWeight] }
        )};
        $isEqualDeclarationTotalItemLine := function($declaration) {(
            $totalLineItem := $declaration.total_line_item ~> $type() = "string" ? $atoi($declaration.total_line_item) : $declaration.total_line_item;
            $itemCount := $count($declaration.items) ?? 0;

            $totalLineItem = $itemCount ? null : {"code": "ERROR_0003", "path": ["tr_export_declaration", "total_line_item"], "args": [$totalLineItem, $itemCount] }
        )};
        $isDeclarationTotalValueEqual := function($declaration) {(
            $itemTotalValue := $reduce($declaration.items, function($acc, $v) {
                $acc + ($v.item_value ? $v.item_value : 0)
            }, 0);
            $declarationTotalValue := $declaration.total_value ? $atoi($declaration.total_value) : 0;

            $round($itemTotalValue, 2) = $declarationTotalValue ? null : { "code":"ERROR_0002", "path": ["tr_export_declaration", "total_value"], "args": [$declarationTotalValue, $itemTotalValue] }
        )};
        $declarationErrorsFirst := $isDeclarationExist ? $filter([
            $isEqualDeclarationTotalGrossWeight(tr_export_declaration),
            $isDeclarationTotalValueEqual(tr_export_declaration),
            $isEqualDeclarationTotalItemLine(tr_export_declaration),
            $isEqualTotalValueDeclarationInvoice(tr_export_declaration),
            $map($.tr_export_declaration.items, function($v, $i) {
                [
                    /* Commodity Code */
                    $isExist($v.commodity_code, {"code": "INVALID_0039", "path": ["tr_export_declaration", "items", $i, "commodity_code"], "args": [$i]}),
                    $max($v.commodity_code, 11, {"code": "INVALID_0039", "path": ["tr_export_declaration", "items", $i, "commodity_code"], "args": [$i]}),

                    /* Country of Origin */
                    $isExist($v.origin, {"code": "INVALID_0042", "path": ["tr_export_declaration", "items", $i, "origin"], "args": [$i]}),
                    $max($v.origin, 2, {"code": "INVALID_0042", "path": ["tr_export_declaration", "items", $i, "origin"], "args": [$i]})
                ]
            }) ~> $customAppend()
        ], function($v) { $v != null }) : [];
        $declarationErrors := $count($declarationErrorsFirst) ? $map($declarationErrorsFirst, function($error) {
            $formatErrorMessage($error)
        }) : [];
        $declarationWarnings := [];


        /* --- */
        /* --- */
        /* --- */
        /* --- */
        /*Atr ---*/
        /* --- */
        /* --- */
        /* --- */
        /* --- */
        $atrErrors := $isAtrExist ? $filter([
        ], function($v) { $v 
        != null }) : [];


        {
            "additional_data": {
                "errors": $count($additionalErrors) = 1 ? [$additionalErrors] : $additionalErrors,
                "warnings": $count($additionalWarnings) = 1 ? [$additionalWarnings] : $additionalWarnings
            },
            "invoice": {
                "errors": $count($invoiceErrors) = 1 ? [$invoiceErrors] : $invoiceErrors,
                "warnings": $count($invoiceWarnings) = 1 ? [$invoiceWarnings] : $invoiceWarnings
            },
            "atr": {
                "errors": [],
                "warnings": []
            },
            "tr_export_declaration": {
                "errors": $count($declarationErrors) = 1 ? [$declarationErrors] : $declarationErrors,
                "warnings": $count($declarationWarnings) = 1 ? [$declarationWarnings] : $declarationWarnings
            }
        }
    )