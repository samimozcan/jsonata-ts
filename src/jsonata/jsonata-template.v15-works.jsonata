/* add additional_data getUnterlage function  11.08.2025 */

(

/* ------------------------------------ */
/* --- UTILS  --- */
/* ------------------------------------ */
/* Compact function to remove null/undefined/false/""/NaN values from array --- @utils */
$_compact;
/* @args date */
$_dateWithoutTime; /* assign function to jsonata */
/* @args source, destination(can be regex), regex flag */
$_removeAfter; /* assign function to jsonata */
/* floor function does not take precious so create new one*/
$_floor;
/* substring function that handles null/undefined */
$_substring;
/* length function that handles null/undefined */
$_length;

/* Remove duplicates from array of objects by field  --- @utils */
$uniqueByField := function($arr, $field) {(
    $uniqueArray := $reduce($arr, function ($acc, $item) {(
        $alreadyExistsInAcc := $acc[$lookup($item, $field)];
        $alreadyExistsInAcc ? $acc : $append($acc, $item)
    )}, []);

    $return := $uniqueArray
)};

/* Custom append function to handle null/undefined values in array --- @utils*/
$customAppend := function($data) {(
      $data = $type($data) = "array" ? $data : $type($data) = "object" ? [$data] : [];

      $response := $reduce($data, $append)
)};

/* Get first non-null/undefined element from array --- @utils*/
$getFirstElement := function($array) { $filter($array, function($v) { $v != null or $v != undefined })[0] };

/* ------------------------------------ */
/* ------------------------------------ */
/* ------------------------------------ */

$A1314 := ["AT","BE","BG","CY","CZ","DE","DK","EE","ES","FI","FR","GR","HR","HU","IE","IT","LT","LU","LV","MT","NL","PL","PT","RO","SE","SI","SK","XI"];
$A1300 := ["CH","GB","GE","IS","LI","MK","NO","TR","UA","XS"];
$1990 := ["1", "2", "3", "4", "5", "7", "8", "9"];

$addressCustomOfficeMap := {"9651": "Aalen","4210": "Albbruck","9501": "Albstadt","7455": "Altötting (Autobahn)","3003": "Am Flughafen (Erfurt)","8801": "Amberg","8351": "Anröchte","8751": "Ansbach-Weißenburg (Ansbach)","3956": "Appenweier","8352": "Arnsberg","8851": "Aschaffenburg","7352": "Aschersleben","3401": "Bad Hersfeld","8005": "Bad Oeynhausen","7458": "Bad Reichenhall-Autobahn","4209": "Bad Säckingen","5853": "Baden-Baden","4105": "Bahnhof (Singen)","4213": "Baltersweil (Dettighofen)","8601": "Bamberg","4058": "Deutsches Zollamt Basel","8602": "Bayreuth","8356": "Beckum","3201": "Bensheim","2102": "Berlin-Flughafen Schönefeld (Schönefeld)","2105": "Berlin-Flughafen Tegel","9657": "Biberach","4101": "Bietingen (Gottmadingen)","9551": "Böblingen","8302": "Bocholt","8051": "Bochum","7152": "Bonn","5301": "Brake","4930": "Braunschweig-Broitzem","2325": "(Bremen)","2452": "Bremerhaven","5852": "Bruchsal","6151": "Brunsbüttel","4214": "Bühl (Klettgau)","4109": "Büßlingen (Tengen)","5111": "Celle","7054": "Charlottenburger Allee (Aachen)","5530": "Chemnitz","8605": "Coburg","8303": "Coesfeld","4501": "Cuxhaven","3230": "(Darmstadt)","4106": "Deißlingen","7361": "Dessau-Ost","8857": "Dettelbach-Mainfrankenpark","4215": "Dogern","9656": "Donautal (Ulm)","7402": "Donauwörth","2152": "Dreilinden (Berlin)","5580": "(Dresden)","7064": "Düren","3752": "Eberswalde","4110": "Ebringen (Gottmadingen)","8001": "Eckendorfer Straße (Bielefeld)","4224": "Eggingen","3002": "Eisenach","5004": "Emden","2701": "Emmerich","8752": "Erlangen-Tennenlohe","4201": "Erzingen (Klettgau)","2651": "Essen","8304": "Eulerstraße (Münster)","3601": "Finsterwalde","5380": "Fledder (Osnabrück)","6133": "Flensburg","2301": "Flughafen (Bremen)","8131": "Flughafen (Dortmund)","2601": "Flughafen (Düsseldorf)","8306": "Flughafen (Greven)","5103": "Flughafen (Langenhagen)","7650": "Flughafen (Hallbergmoos)","8755": "Flughafen (Nürnberg)","9304": "Flughafen (Saarbrücken)","9555": "Flughafen (Filderstadt)","5552": "Flughafen Dresden","7154": "Flughafen Köln/Bonn","5604": "Flughafen Leipzig","8380": "Flughafen Paderborn/Lippstadt","3603": "Forst-Autobahn","3302": "Fracht (Frankfurt)","3652": "Frankfurt (Oder) – Autobahn","3358": "Frankfurt am Main-Osthafen","3954": "Freiburg","9402": "Friedrichshafen","3430": "Fulda","3656": "Fürstenwalde","8902": "Furth im Wald","4216": "Fützen (Blumberg)","4111": "Gaienhofen (Gottmadingen)","4112": "Gailingen","7602": "Garching-Hochbrück","8052": "Gelsenkirchen","6653": "Germersheim","7401": "Göggingen (Augsburg)","9652": "Göppingen","5052": "Goslar","5080": "Göttingen","4115": "Gottmadingen","4051": "Grenzacherhorn (Grenzach-Wyhlen)","7153": "Gummersbach","4217": "Günzgen (Hohentengen am Hochrhein)","9452": "Heilbronn","8756": "Hafen (Nürnberg)","4603": "Hafencity (Hamburg)","9552": "Hafen (Stuttgart)","8101": "Hagen","6756": "Hahn-Flughafen (Lautzenhausen)","7501": "Hallbergmoos","7362": "Halle (Saale)","4701": "Hamburg-Flughafen","4851": "(Hamburg)","5053": "Hameln","3352": "Hanau","5102": "Hannover-Nord","5901": "Heidelberg","6302": "Heiligenhafen","7053": "Heinsberg","4506": "Helgoland","4961": "Helmstedt-Autobahn","4218": "Herdern (Hohentengen am Hochrhein)","5055": "Hildesheim","5756": "Hirschfeld","3354": "Höchst (Frankfurt am Main)","8715": "Hof-Marktredwitz (Hof)","7551": "Hörbranz-Autobahn","6155": "Husum","6752": "Idar-Oberstein","7403": "Ingolstadt","4060": "Inzlingen","3004": "Jena","4203": "Jestetten","6501": "Kaiserslautern","5880": "(Karlsruhe)","3454": "Kassel","7553": "Kempten (Allgäu)","6207": "(Kiel)","7157": "Wahn (Köln)","4005": "Konstanz-Autobahn","4001": "Konstanz-Emmishofer Tor","4002": "Konstanz-Güterbahnhof","4003": "Konstanz-Kreuzlinger Tor","4010": "Konstanz-Paradieser Tor","5330": "Kreyenbrück (Oldenburg)","9102": "Laage","4204": "Laufenburg","4207": "Laufenburg-Stadt","8004": "Lemgo","5356": "Lingen","5687": "Löbau","5355": "Lohne","4205": "Lottstetten","8008": "Lübbecke","6333": "Lübeck","8104": "Lüdenscheid","9453": "Ludwigsburg","3701": "Ludwigshafen am Rhein","9101": "Ludwigslust","5230": "Lüneburg","7380": "Magdeburg-Rothensee","6504": "Mainz","5904": "Mannheim","3452": "Marburg","2101": "Marzahn (Berlin)","7561": "Memmingen","5110": "Messe (Hannover)","6310": "Mölln","2904": "Mönchengladbach","9154": "Mukran (Sassnitz)","9030": "Neubrandenburg","4102": "Neuhaus (Blumberg)","2906": "Neuss","7202": "Köln-West","2604": "Nord (Düsseldorf)","5502": "Nossen","9503": "Nürtingen","4605": "Oberelbe (Hamburg)","3356": "Oberursel","4117": "Öhningen","8103": "Ost (Dortmund)","8355": "Paderborn","5008": "Papenburg","7703": "Passau","5858": "Pforzheim","6156": "Pinneberg","6502": "Pirmasens","7504": "Plattling","9004": "Pomellen","9404": "Ravensburg","4221": "Reckingen (Küssaberg)","8804": "(Regensburg)","4012": "Reichenau","7755": "Reischenhart (Raubling)","2607": "Reisholz","6206": "Rendsburg","9530": "Reutlingen","8305": "Rheine","4054": "Rheinfelden","4062": "Rheinfelden-Autobahn","6551": "Rheinhafen (Koblenz)","4222": "Rheinheim (Küssaberg)","4103": "Rielasingen","9104": "Rostock","4223": "Rötteln (Hohentengen am Hochrhein)","2656": "Ruhrort (Duisburg)","9303": "(Saarbrücken)","4119": "Schlatt am Randen (CH-Thayngen)","2151": "Schöneberg (Berlin)","5351": "Schüttorf","2901": "Schwanenhaus (Nettetal)","8853": "Schweinfurt-Londonstraße","8106": "Siegen","5202": "Soltau","5203": "Stade","7359": "Stendal","4053": "Stetten (Lörrach)","2702": "Straelen-Autobahn","9180": "Stralsund-Dänholm","4206": "Stühlingen","7701": "Suben-Autobahn (Österreich)","9456": "Tauberbischofsheim","5603": "Taucha","6753": "Trier-Ehrang","2903": "Uerdingen","9459": "Untermünkheim","3702": "Velten","5204": "Verden","4208": "Waldshut","4120": "Wangen (Bodensee) (Öhningen)","4219": "Wangental (Jestetten)","8904": "Weiden-Waidhaus (Weiden in der Oberpfalz)","4055": "Weil am Rhein-Autobahn","4056": "Weil am Rhein-Friedlingen","4061": "Weil am Rhein-Ost","4057": "Weil am Rhein-Otterbach","4083": "Weil am Rhein","7751": "Weilheim","3453": "Wetzlar","4121": "Wiechs-Dorf (CH-Altdorf)","4122": "Wiechs-Schlauch (Tengen)","3202": "Wiesbaden","6203": "Wik (Kiel)","5310": "Wilhelmshaven","9556": "Winnenden","9103": "Wismar","4906": "Wolfsburg","9152": "Wolgast","2956": "Wuppertal","9554": "Zuffenhausen (Stuttgart)"};
$qpCountry := ["TR"]; /*TODO: Bu liste guncellenecek.*/

$isDeclarationExist := $exists($.tr_export_declaration) and $type($.tr_export_declaration) = "object";
$isInvoiceExist := $exists($.invoice) and $type($.invoice) = "array" and $count($.invoice) > 0;
$isCmrExist := $exists($.cmr) and $type($.cmr) = "object";
$isAtrExist := $exists($.atr) and $type($.atr) = "object";
$isTransitDeclarationExist := $exists($.transit_declaration) and $type($.transit_declaration) = "object";
$isCombineItemsExist := $exists($.combine_items) and $type($.combine_items) = "object" and $count($.combine_items.items) > 0;
$isCombineItemsCreatedFromInvoice := $isCombineItemsExist and $.combine_items.items[0].reference_item_details[0].source = 'invoice';
$isCommercialInvoiceExist := $exists($.commercial_invoice) and $type($.commercial_invoice) = "array";
$isFtzExist := $exists($.ftz) and $type($.ftz) = "object" and $.ftz.shipment.id ?: null;

/*ZollrechtlicherStatus condition @return "CO" "EU" "IM"*/
/*https://atez.atlassian.net/wiki/spaces/relayhub/pages/3476291624/Relayhub+Algorithm+i+in+Alan+Dakosy+Analizleri*/
$getZollrechtlicherStatus := function($departureCountry, $documentDivision, $isAtrExist) {(
  $COStatus := $departureCountry in $A1314 and $documentDivision = "6" ? "CO" : null;
  $EUStatus := $departureCountry in $1300 or ($departureCountry in $qpCountry and $isAtrExist) ? "EU" : null;
  $IMStatus := "IM"; /* working like else */

  $response := $COStatus ? $COStatus : $EUStatus ? $EUStatus : $IMStatus
)};

/*
  Mode of transport mapping
    10, 12, 16, 18 ->	02 – Schiff (Gemi)	1 – Sea transport
    20, 21, 24, 25 ->	03 – Waggon (Vagon)	2 – Rail transport
    30, 16, 17, 23, 90 ->	01 – LKW (Kamyon / Tır)	3 – Road transport
    40 ->	04 – Flugzeug (Uçak)	4 – Air transport
    50 ->	07 – Andere (Posta, özel taşıma)	5 – Postal consignments
    70 ->	07 – Andere	7 – Fixed installations
    80, 81, 82, 83, 84 ->	02 – Schiff (Su taşıtı)	8 – Inland waterway transport
    90, 99 ->	01 – LKW / 05 – PKW / 07 – Andere	9 – Own propulsion
*/
$getBefoerderungsmittelGrenzeArt := function($modeOfTransport) {(
  $ship := ["10", "12", "16", "18"];
  $rail := ["20", "21", "24", "25"];
  $road := ["30", "16", "17", "23", "90"];
  $air := ["40"];
  $postal := ["50"];
  $fixed := ["70"];
  $inlandWaterway := ["80", "81", "82", "83", "84"];
  $ownPropulsion := ["90", "99"];

  $response := $modeOfTransport in $ship ? "02"
    : $modeOfTransport in $rail ? "03"
      : $modeOfTransport in $road ? "01"
        : $modeOfTransport in $air ? "04"
          : $modeOfTransport in $postal ? "07"
            : $modeOfTransport in $fixed ? "07"
              : $modeOfTransport in $inlandWaterway ? "02"
                : $modeOfTransport in $ownPropulsion ? "01"
                  : "01";
)};


/*
* ------------------------------------
* Constans
* ------------------------------------
*/
$totalAmount := $getFirstElement([
  $.tr_export_declaration.total_value,
  $.invoice.invoice_total_amount,
  $.commercial_invoice[0].invoice_total_amount,
  $.ftz.total_value
]);
$totalNetWeight := $getFirstElement([
  $isDeclarationExist ? $.tr_export_declaration.items.net_weight ~> $_compact() ~> $sum() : null,
  $isInvoiceExist ? $.invoice.invoice_total_net_weight ~> $_compact() ~> $sum() : null,
  $isCommercialInvoiceExist ? $.commercial_invoice.invoice_total_net_weight ~> $_compact() ~> $sum() : null,
  $isCmrExist and $.cmr.total_net_weight ? $round($.cmr.total_net_weight, 1) : null,
  0
]);
$totalGrossWeight := $getFirstElement([
  $isDeclarationExist ? $.tr_export_declaration.items.gross_weight ~> $_compact() ~> $sum() : null,
  $isCmrExist and $.cmr.total_gross_weight > 0 ? $round($.cmr.total_gross_weight, 1) : null,
  $isInvoiceExist ? $.invoice.invoice_total_gross_weight ~> $_compact() ~> $sum() : null,
  $isCommercialInvoiceExist ? $.commercial_invoice.invoice_total_gross_weight ~> $_compact() ~> $sum() : null,
  $isAtrExist and $.atr.total_gross_weight > 0 ? $round($.atr.total_gross_weight, 1) : null,
  $isFtzExist and $.ftz.total_gross_weight > 0 ? $round($.ftz.total_gross_weight, 1) : null,
  0
]);
$totalPackageQuantity := $getFirstElement([
  $isDeclarationExist ? $.tr_export_declaration.total_package ~> $number() ~> $round(0) : null,
  $isInvoiceExist ? $.invoice.invoice_total_package_quantity ~> $_compact() ~> $sum() ~> $round(0) : null,
  $isCommercialInvoiceExist ? $.commercial_invoice.invoice_total_package_quantity ~> $_compact() ~> $sum() ~> $round(0) : null,
  $isFtzExist ? $.ftz.total_package ~> $number() ~> $round(0) : null,
  0
]);
$totalAmountCurrency := $getFirstElement([
  $.tr_export_declaration.total_value_curr,
  $.invoice.invoice_currency,
  $.invoice[0].invoice_currency,
  $.commercial_invoice[0].invoice_currency,
  $.ftz.total_value_curr
]);
$globalDeliveryTerm := $getFirstElement([
  $.tr_export_declaration.delivery_term,
  $.invoice[0].invoice_delivery_term,
  $.commercial_invoice[0].invoice_delivery_term,
  $.ftz.shipment.positions[0].delivery_term
]);
$globalModeOfTransport := $getFirstElement([
  $.tr_export_declaration.border_transport_mode,
  $.transit_declaration.border_transport_mode,
  $.ftz.mode_of_transport
]);
$globalDispatchCountry := $getFirstElement([
  $isDeclarationExist and $_length($.tr_export_declaration.dispatch_country) = 2 ? $.tr_export_declaration.dispatch_country : null,
  $isInvoiceExist and $_length($.invoice[0].invoice_country_of_dispatch) = 2 ? $.invoice[0].invoice_country_of_dispatch : null,
  $isCommercialInvoiceExist and $_length($.commercial_invoice[0].invoice_country_of_dispatch) = 2 ? $.commercial_invoice[0].invoice_country_of_dispatch : null,
  $_length($.dispatch_country) = 2 ? $.dispatch_country : null,
  $isFtzExist and $_length($.ftz.dispatch_country) = 2 ? $.ftz.dispatch_country : null,
  "TR"
]);
$globalDestionationCountry := $getFirstElement([
  $isDeclarationExist and $_length($.tr_export_declaration.destination_country) = 2 ? $.tr_export_declaration.destination_country : null,
  $isInvoiceExist and $_length($.invoice[0].invoice_shipment_country_of_origin) = 2 ? $.invoice[0].invoice_shipment_country_of_origin : null,
  $isCommercialInvoiceExist and $_length($.commercial_invoice[0].invoice_shipment_country_of_origin) = 2 ? $.commercial_invoice[0].invoice_shipment_country_of_origin : null,
  $_length($.destination_country) = 2 ? $.destination_country : null,
  $isFtzExist and $_length($.ftz.destination_country) = 2 ? $.ftz.destination_country : null,
  "DE"
]);
$globalExporterAddress := $getFirstElement([
  $.invoice[0].invoice_supplier_address,
  $.atr.exporter_address,
  $.cmr.exporter_address,
  ""
]);
$globalExporterCountry := $getFirstElement([
  $.invoice[0].invoice_supplier_country,
  $.atr.exporter_country,
  $.cmr.exporter_country,
  "TR"
]);
$globalExporterName := $getFirstElement([
  $.invoice[0].invoice_supplier_name,
  $.atr.sender,
  $.cmr.sender,
  $.ftz.sender,
  $.tr_export_declaration.sender,
  "Exporter Name"
]);
$globalExporterCity := $getFirstElement([
  $.atr.exporter_city,
  $.cmr.exporter_city,
  "Istanbul"
]);
$globalBuyerName := $getFirstElement([
  $.ftz.buyer,
  $.tr_export_declaration.buyer,
  "Buyer Name"
]);

/* Total item counts */
$totalInvoiceItemCount := $isInvoiceExist ? $reduce($.invoice, function($acc, $inv){$acc + ($count($inv.items)) }, 0) : 0;
$totalCommercialInvoiceItemCount := $isCommercialInvoiceExist ? $reduce($.commercial_invoice, function($acc, $inv){$acc + ($count($inv.items)) }, 0) : 0;
$totalDeclarationItemCount := $isDeclarationExist ? $count($.tr_export_declaration.items) : 0;
$totalFtzItemCount := $isFtzExist ? $count($.ftz.shipment.positions) : 0;

/* Unterlage Root Not item*/
$getUnterlage := function($procedureCodeRequested) {(
  $invoiceUnterlage := $isInvoiceExist and $type($.invoice) = "object" ? {
    "Bereich": "4", /* default */
    "Art":  "N380", /* default */
    "Nummer": $.invoice.invoice_id, /*  */
    "DatumAusstellung": $_dateWithoutTime($.invoice.invoice_date) /*  */
  } : $isInvoiceExist and $type($.invoice) = "array" ? $map($.invoice, function($v, $i) {(
    {
      "Bereich": "4", /* default */
      "Art":  "N380", /* default */
      "Nummer": $v.invoice_id, /*  */
      "DatumAusstellung": $_dateWithoutTime($v.invoice_date) /*  */
    }
  )}) : undefined;

  $return := $procedureCodeRequested = "42" ? [$invoiceUnterlage] ~> $filter(function($v) { $exists($v) }) : undefined;
)};

/* item document response unterlage*/
$itemUnterlage := function($item) {(
  $invoiceDocument := $isInvoiceExist ? $.invoice ~> $map((function($item) {
    {
      "Bereich": "4", /* default */
      "Art":  "N380", /* default */
      "Nummer": $item.invoice_id, /*  */
      "DatumAusstellung": $_dateWithoutTime($item.invoice_date), /*  */
      "VorlageKz": "1" /* default */
    }
  })) : undefined;

  $atrDocument := $isAtrExist ? {
    "Bereich": "6", /* default */
    "Art":  "N018", /* default */
    "Nummer": $.atr.id, /*  */
    "DatumAusstellung": $_dateWithoutTime($.atr.date), /*  */
    "VorlageKz": "1" /* default */
  } : undefined;

  $cmrDocument := $isCmrExist ? {
    "Bereich": "4", /* default */
    "Art":  "7HHF", /* default */
    "Nummer": "CMR", /* TODO: AI ekibinden okunmasi istendi sornasinda duzeltilecek @samimozcan @alican  */
    "DatumAusstellung": $_dateWithoutTime($.cmr.date), /*  */
    "VorlageKz": "1" /* default */
  } : undefined;

  $response := [$invoiceDocument, $atrDocument, $cmrDocument] ~> $customAppend() ~> $filter(function($v) { $exists($v) });
)};

/* begünstigung beantragt code ***only work if atr exists otherwise return 100 @emre */
$beguenstigungBeantragtCode := function() {(
  $response := $isAtrExist ? 400 : 100 /* eur varsa 400 gibisinden bir kosul olacak onun donusu burasi setlenecek */
)};

/* If not exist return 1 or floor arg value */
$aHStatWert := function($value) {(
  $response := $value = null or $value = undefined ? null : $value < 1 ? 1 : $value ~> $_floor()
)};

/* AbzugHinzurechnung */
$abzugHinzurechnung := function($args) {(
  $response := {
    "ArtCode": "R", /* default */
    "Betrag": $args.amount ~> $round(2), /*$args.amount, */
    "Waehrung": $args.currency,
    "KursVereinbartKz": "0" /* default */
  }
)};

/* WarenNummerEZT *** */
$warenNummerEZT := function($commodityCode) {(
  $response := $type($commodityCode) = "string" and $_length($commodityCode) > 11 ? "" : $commodityCode;
)};

$allDeclarationItemsCommodityCodeSuccess := $isDeclarationExist ? 
  $filter($.tr_export_declaration.items, function($v) { $type($v.commodity_code) != "string" or $_length($v.commodity_code) > 11 }) ~> $count() = 0
    : false;
$allInvoiceItemsCommodityCodeSuccess :=
  $isInvoiceExist ?
  (
    $reduce($.invoice, function($acc, $inv){
      $acc + (
        $filter($inv.items, function($v){
          $type($v.invoice_item_commodity_code) != "string"
          or $_length($v.invoice_item_commodity_code) > 11
        }) ~> $count()
      )
    }, 0) = 0
  )
  : false;
$allCommercialInvoiceItemsCommodityCodeSuccess :=
  $isCommercialInvoiceExist ?
  (
    $reduce($.commercial_invoice, function($acc, $inv){
      $acc + (
        $filter($inv.items, function($v){
          $type($v.invoice_item_commodity_code) != "string"
          or $_length($v.invoice_item_commodity_code) > 11
        }) ~> $count()
      )
    }, 0) = 0
  )
  : false;
$declarationCommodityCondition := $isDeclarationExist and $allDeclarationItemsCommodityCodeSuccess or ($allInvoiceItemsCommodityCodeSuccess != false and $allCommercialInvoiceItemsCommodityCodeSuccess != false);
$invoiceCommodityCondition := $isInvoiceExist and $allInvoiceItemsCommodityCodeSuccess or ($allDeclarationItemsCommodityCodeSuccess != false and $allCommercialInvoiceItemsCommodityCodeSuccess != false);

/* Net Weight Function*/
$getNetWeightForItem := function($netWeight, $totalItemCount) {(
  $perWeight := $totalNetWeight and $totalItemCount > 0 ? $totalNetWeight / $totalItemCount : 0;

  $response := $netWeight != null and $netWeight > 0 ? $netWeight ~> $round(1) : $perWeight ~> $round(1)
)};

$getWarenPosition := function($procedureCodeRequested) {(
    $response := $isCombineItemsExist ? $map($.combine_items.items, function($v, $i) {
      {
            "Positionsnummer": $v.combined_item_no,
            "WarenNummerEZT": $warenNummerEZT($v.combined_item_commodity_code),
            "WarenBezeichnung": $v.combined_item_description ~> $_removeAfter("1?-?unregistriert") ~> $substring(0, 240),
            "VerfahrenscodeVorangegangenesVerfahren": $procedureCodeRequested ? "00" : null, 
            "UrsprungslandCode": $getFirstElement([$v.combined_item_country_of_origin, 'TR']), 
            "Eigenmasse": $getNetWeightForItem($v.combined_item_net_weight, $.combine_items.meta.total_merged_items), /* Burasi float kabul etmiyor ondan dolayi round atilacak simdilik @alican @samim */
            "AHStatMenge": $v.combined_item_quantity ? $round($v.combined_item_quantity, 3) : null,
            "AHStatMengeMasseinheit": $v.combined_item_unit_type,
            "AHStatWert": $aHStatWert($v.combined_item_statistical_value), /*default for now, eger 1 den kucukse 1 olacak, yoksa floor atilacak*/
            "PackstueckAnzahl": $i = 0 ? $totalPackageQuantity : undefined,
            "PackstueckArt": $i = 0 ? "PK" : undefined, /* default */
            "PackstueckZeichen": $i = 0 ? "OHNE" : undefined, /* default */
            "BeguenstigungBeantragtCode": $beguenstigungBeantragtCode(),
            "AHStatWertManuellKZ": "N", /* default to disable manual price setting */
            "Unterlage": $itemUnterlage($v),
            "Zollwert": $.combine_items.items.combined_item_total_amount ? $.combine_items.items.combined_item_total_amount ~> $_compact() ~> $sum() ~> $round(2) : undefined,
            "AbzugHinzurechnung": $abzugHinzurechnung({
              "amount": $v.combined_item_total_amount,
              "currency": $totalAmountCurrency /* TODO: Burasi hallediverilecek. @samimozcan*/
            })
          }
    }) : $isDeclarationExist and $declarationCommodityCondition ? $map($.tr_export_declaration.items, function($v, $i) {
          {
            "Positionsnummer": $i + 1,
            "WarenNummerEZT": $warenNummerEZT($v.commodity_code),
            "WarenBezeichnung": $v.commodity_description ~> $_removeAfter("1?-?unregistriert") ~> $substring(0, 240),
            "VerfahrenscodeVorangegangenesVerfahren": $procedureCodeRequested ? "00" : null, 
            "UrsprungslandCode": $getFirstElement([$v.origin, 'TR']), 
            "Eigenmasse": $getNetWeightForItem($v.net_weight, $totalDeclarationItemCount), /* Burasi float kabul etmiyor ondan dolayi round atilacak simdilik @alican @samim */
            "AHStatMenge": $v.quantity and $type($v.quantity) = "number" ? $round($v.quantity, 3) : null,
            "AHStatMengeMasseinheit": $v.unit_of_measure, 
            "AHStatWert": $aHStatWert($v.statistical_value), /*default for now, eger 1 den kucukse 1 olacak, yoksa floor atilacak*/
            "PackstueckAnzahl": $i = 0 ? $totalPackageQuantity : undefined,
            "PackstueckArt": $i = 0 ? "PK" : undefined, /* default */
            "PackstueckZeichen": $i = 0 ? "OHNE" : undefined, /* default */
            "BeguenstigungBeantragtCode": $beguenstigungBeantragtCode(), 
            "AHStatWertManuellKZ": "N", /* default to disable manual price setting */
            "Unterlage": $itemUnterlage($v),
            "Zollwert": $.tr_export_declaration.total_value,
            "AbzugHinzurechnung": $abzugHinzurechnung({
              "amount": $v.item_value,
              "currency": $totalAmountCurrency
            })
          }
    })
    : $isInvoiceExist and $invoiceCommodityCondition ?
        $map(invoice, function($v, $i) {(
          $map($v.items, function($vv, $ii) {(
            {
              "Positionsnummer": "default", /* will be set later because of many invoices */
              "WarenNummerEZT": $warenNummerEZT($vv.invoice_item_commodity_code), 
              "WarenBezeichnung": $vv.invoice_item_description ~> $_removeAfter("1?-?unregistriert") ~> $substring(0, 240),
              "VerfahrenscodeVorangegangenesVerfahren": $procedureCodeRequested ? "00" : null, 
              "UrsprungslandCode": $getFirstElement([$vv.invoice_item_country_of_origin, 'TR']), 
              "Eigenmasse": $getNetWeightForItem($vv.invoice_item_net_weight, $totalInvoiceItemCount), /*default for now  @alican For AÜV and ATÜV goods, at least 1 kg must be declared. */
              "AHStatMenge": $vv.invoice_item_quantity ? $round($vv.invoice_item_quantity, 3) : null, /* statistical quantity not exist in invoice */
              "AHStatMengeMasseinheit": $vv.invoice_item_unit_type,
              "AHStatWert": null, /* statistical value */
              "PackstueckAnzahl": $ii = 0 ? $totalPackageQuantity : undefined, 
              "PackstueckArt": $ii = 0 ? "PK" : undefined, /*default*/
              "PackstueckZeichen": $ii = 0 ? "OHNE" : undefined, /*default for now*/
              "BeguenstigungBeantragtCode": $beguenstigungBeantragtCode(), 
              "AHStatWertManuellKZ": "N", /* default to disable manual price setting */
              "Unterlage": $itemUnterlage($v),
              "Zollwert": $.invoice.invoice_total_amount ? $.invoice.invoice_total_amount ~> $_compact() ~> $sum() : undefined, 
              "AbzugHinzurechnung": $abzugHinzurechnung({
                "amount": $vv.invoice_item_total_amount,
                "currency": $totalAmountCurrency
              })
            }
          )})
        )}) ~> $customAppend() ~> function($data) {$map($data, function($v, $i) {($merge([$v, {"Positionsnummer": $i + 1}]))})}
     : $isCommercialInvoiceExist ? 
        $map($.commercial_invoice, function($v, $i) {(
          $map($v.items, function($vv, $ii) {(
            {
              "Positionsnummer": "default", /* will be set later because of many invoices */
              "WarenNummerEZT": $warenNummerEZT($vv.invoice_item_commodity_code), 
              "WarenBezeichnung": $vv.invoice_item_description ~> $_removeAfter("1?-?unregistriert") ~> $substring(0, 240),
              "VerfahrenscodeVorangegangenesVerfahren": $procedureCodeRequested ? "00" : null, 
              "UrsprungslandCode": $getFirstElement([$vv.invoice_item_country_of_origin, 'TR']), 
              "Eigenmasse": $getNetWeightForItem(null, $totalCommercialInvoiceItemCount), /*default for now  @alican For AÜV and ATÜV goods, at least 1 kg must be declared. */
              "AHStatMenge": $vv.invoice_item_quantity ? $round($vv.invoice_item_quantity, 3) : undefined,
              "AHStatMengeMasseinheit": $vv.invoice_item_unit_type, 
              "AHStatWert": null, /* statistical value */
              "PackstueckAnzahl": $ii = 0 ? totalPackageQuantity : undefined, 
              "PackstueckArt": $ii = 0 ? "PK" : undefined, /*default*/
              "PackstueckZeichen": $ii = 0 ? "OHNE" : undefined, /*default for now*/
              "BeguenstigungBeantragtCode": $beguenstigungBeantragtCode(), 
              "AHStatWertManuellKZ": "N", /* default to disable manual price setting */
              "Unterlage": $itemUnterlage($v),
              "Zollwert": $.commercial_invoice.invoice_total_amount ? $.commercial_invoice.invoice_total_amount ~> $_compact() ~> $sum() : undefined, 
              "AbzugHinzurechnung": $abzugHinzurechnung({
                "amount": $vv.invoice_item_total_amount,
                "currency": $totalAmountCurrency
              })
            }
          )})
        )}) ~> $customAppend() ~> function($data) {$map($data, function($v, $i) {($merge([$v, {"Positionsnummer": $i + 1}]))})}
        : $isFtzExist ? $map($.ftz.shipment.positions, function($v, $i) {
          {
            "Positionsnummer": $i + 1,
            "WarenNummerEZT": $warenNummerEZT($v.commodity_code),
            "WarenBezeichnung": $v.commodity_description ~> $_removeAfter("1?-?unregistriert") ~> $substring(0, 240),
            "VerfahrenscodeVorangegangenesVerfahren": $procedureCodeRequested ? "00" : null, 
            "UrsprungslandCode": $getFirstElement([$v.origin, 'TR']), 
            "Eigenmasse": $getNetWeightForItem($v.net_weight, $count($.ftz.shipment.positions)), /* Burasi float kabul etmiyor ondan dolayi round atilacak simdilik @alican @samim */
            "AHStatMenge": $v.quantity and $type($v.quantity) = "number" ? $round($v.quantity, 3) : null,
            "AHStatMengeMasseinheit": $v.unit_of_measure, 
            "AHStatWert": $aHStatWert($v.statistical_value), /*default for now, eger 1 den kucukse 1 olacak, yoksa floor atilacak*/
            "PackstueckAnzahl": $i = 0 ? $totalPackageQuantity : undefined,
            "PackstueckArt": $i = 0 ? "PK" : undefined, /* default */
            "PackstueckZeichen": $i = 0 ? "OHNE" : undefined, /* default */
            "BeguenstigungBeantragtCode": $beguenstigungBeantragtCode(), 
            "AHStatWertManuellKZ": "N", /* default to disable manual price setting */
            "Unterlage": $itemUnterlage($v),
            "Zollwert": $.ftz.total_value,
            "AbzugHinzurechnung": $abzugHinzurechnung({
              "amount": $v.value,
              "currency": $totalAmountCurrency
            })
          }
    })
        : undefined
)};

/* Delivery place */
$lieferbedingungOrt := function($city) {(
  $deliveryTerm := $globalDeliveryTerm;

  $deliveryTermFirstChar := $substring($deliveryTerm, 0, 1);

  $conditionCD := $deliveryTermFirstChar in ["C", "D"] ? true : false;
  $conditionFE := $deliveryTermFirstChar in ["F", "E"] ? true : false;

  /*TODO: actually for CFR, CIF, CIP, CPT, DAP, DPU, or DDP it should be the name of the Delivery place. Most times this would be importers place. For example CFR Berlin or DAP Passau.
            For delivery terms EXW, FCA, FAS, or FOB it should be the name of the city of dispatch.
            For example EXW Istanbul , FOB Izmir..
            If the AI cannot read it, you can use either the Importers place or Exporters place (use data from exporter / importer field)*/
  $response := $conditionFE ? $.tr_export_declaration.delivery_place : $conditionCD ? $city ?: $.tr_export_declaration.delivery_place : $.tr_export_declaration.delivery_place
)};

$lieferbedingungSchluessel := function() {(
  $deliveryTerm := $globalDeliveryTerm;

  $deliveryTermFirstChar := $substring($deliveryTerm, 0, 1);

  $conditionCD := $deliveryTermFirstChar in ["C", "D"] ? true : false;
  $conditionFE := $deliveryTermFirstChar in ["F", "E"] ? true : false;

  $response := $conditionFE ? "1" : $conditionCD ? "3" : ""
)};

{
  "FreierVerkehrAktVeredelUmwandlung": {
    "Transaktion": {
      "IOPartner": $.additional_data.transaction.ioPartner,
      "IODivision3": $.additional_data.transaction.ioDivision3,
      "IOReferenz": $.additional_data.transaction.ioReference,
      "IODatumZeit": $now(),
      "Version": "004" /* Default */
    },
    "EinzelAnmeldung": $map($.additional_data.declaration, function($v, $i) {
      {
        "ObjektIdentifizierung": {
          "ObjektName": $v.objectIdentification.objectName,
          "ObjektAlias": $v.objectIdentification.objectAlias,
          "AnmeldungArt": $v.objectIdentification.declarationType,
          "BezugsnummerVorblendung": $v.objectIdentification.referenceNumberOverlay,
          "ObjektAktion": "CREATE",
          "Bearbeiter": $v.objectIdentification.username
        },
        "KopfDaten": {
          "AdressierteZollstelle": $v.headerData.addressedCustomsOffice,
          "AnmelderIstEmpfaenger": $v.headerData.declarantIsConsignee ? "J" : "N",
          "VertretungsverhaeltnisCode": $v.headerData.representationRelationshipCode ?: "" /*$getFirstElement([$v.headerData.representationRelationshipCode, ""])*/, /* TODO: Burasi simdilik default atildi bakilacak */
          "Vorsteuerabzug": $v.headerData.inputTaxDeduction ? "J" : "N",
          "NameAnmeldenderBearbeiter": $v.headerData.agentContact.contactPersonName,
          "TelefonnummerAnmeldenderBearbeiter": $v.headerData.agentContact.contactPersonPhoneNumber,
          "StellungAnmeldenderBearbeiter": $v.headerData.agentContact.contactPersonPosition,
          "EmailAdresseAnmeldenderBearbeiter": $v.headerData.agentContact.contactPersonEmail,
          "FiskalvertretungKz": $v.headerData.procedureCodeRequested = "42" ? "J" : ($v.headerData.procedureCodeRequested = "40" ? "N" : null),
          "ZusammenfassendeMeldungKzStdKto": $v.headerData.procedureCodeRequested = "42" ? "J" : undefined, /* TODO: Burası test edilecek...!!!*/
          "IntrastatKzStdKto": $v.headerData.procedureCodeRequested = "42" ? "J" : undefined, /* TODO: Burası test edilecek...!!!*/
          "Ausstellungsort": $lookup($addressCustomOfficeMap, $v.headerData.addressedCustomsOffice), /* TODO: Test ...!!!*/
          "ZollrechtlicherStatus": $getZollrechtlicherStatus($v.headerData.departureCountry ?: $.dispatch_country, $v.headerData.base.documentDivision, $exists(atr)),
          "GesamtRohMasse": $totalGrossWeight ~> $round(1), /* DONE @alican burasi yoksa cmr den alinilicak denilmis ama baska gross goremedim, cmr tarafindaki gross weight ai service den total_weight olarak donuyor @alican @samim Ayni sekilde burasi da integer istiyor*/
          "VerfahrenBeantragtCode": $v.headerData.procedureCodeRequested, /*ZollrechtlicherStatus eger CO ise 49 olmasi gerek ama burada yazilmayaca, burasi validation a tasinacak...*/
          "VerkehrszweigInland": $.transit_declaration.type_of_transport ?: null, /* : Template datadan bu alan dolduruluyor mu kontrol edilecek. A1990 a bakilarak bir seyler yapilacak gibi.*/
          "VerkehrszweigGrenze":$globalModeOfTransport ? $substring($globalModeOfTransport, 0, 1) : "3", /* : @Alican tolgalar ile gorusecek bunun icin */
          "VersendungslandCode": $globalDispatchCountry ?: $v.headerData.departureCountry and $_length($v.headerData.departureCountry) = 2 ? $v.headerData.departureCountry,
          "Bestimmungsbundesland": $v.headerData.destinationFederalState, /*Aviso header dan gelmeye bilir, o durumda biz koyucagiz*/
          "Bestimmungsland": $globalDestionationCountry ?: $v.headerData.destinationCountry, /*TODO: validation icin A1314 e varmi yok mu kontrol saglanacak. eger Bestimmungsbundesland alan 25 ise ve burasi DE ise hata donecek, validation icin*/
          "KennzeichenNameBefoerderungsmittelAnkunft": $v.headerData.transportMeansArrivalIdentity,
          "BefoerderungsmittelGrenzeStaatszugehoerigkeitCode": $v.headerData.transportMeansNationalityCode,
          "BefoerderungsmittelGrenzeArt": $getBefoerderungsmittelGrenzeArt($v.headerData.modeOfTransport), /* Turkiye Almanya arasi daima kara yolu kullanilacak */
          "LieferbedingungCode": $globalDeliveryTerm,
          "LieferbedingungSchluessel": $lieferbedingungSchluessel(), 
          "LieferbedingungOrt": $lieferbedingungOrt($v.addresses[0].city), /* () AI den alinacak. Kurali eklenecektir. Beyannameden alinacak. */
          "VorpapierArtCode": $v.headerData.previousAdministrativeReferenceType or $isTransitDeclarationExist ? "T1" : "OHNE", /*TODO: Burasi sorulacak, default `T1` kalsin mi ? @Alican to @Andreas*/
          "VorpapierNr": $v.headerData.previousAdministrativeReferenceNumber ? $v.headerData.previousAdministrativeReferenceNumber : $isTransitDeclarationExist ? $.transit_declaration.transit_id : null, /*TODO: Burasida sorulacak, eger yukaris `T1` yazilirsa asagisi ne yazmasi gerek ? */
          "ArtGeschaeftCode": $v.headerData.procedureCodeRequested = "42" ? "71" : "11", /* eger `procedureCodeRequested` = "42" ise burasi 71 olacak yoksa 11 olarak setlendi TODO: Buranin kosulu biraz karisik sonradan kontrol saglanacak @Alican @Tolga @Emre  */
          "StatistikStatus": null, /* Template data olarak dolacak */
          "Rechnungspreis": $totalAmount, /* AI tarafindan gelecek, ihracat beyannamesi yoksa faturadan. */
          "Rechnungswaehrung": $totalAmountCurrency, /* AI tarafindan gelecek, ihracat beyannamesi yoksa faturadan. Buranin validasyonu icin IO400 alani kullanilacak. */
          "Unterlage": $getUnterlage($v.headerData.procedureCodeRequested),
          "Zahlungsart": $v.headerData.procedureCodeRequested = "42" ? "D" : "E", /* A2060 dan kontrol.  */
          "Adressen": $map($v.addresses, function($a) { /* Eger EORI varsa digerlerine ihtiyac yok, lakin yoksa gelmeleri gerekiyor, validasyona yazilacak. */
            {
              "AdressTyp": $a.addressType,
              "AdressCode": "1",
              "TeilnehmerEORI": $a.participantEORI,
              "TeilnehmerNLNR": $a.participantSubsidiaryNumber,
              "NameFirma": $substring($a.companyName ?: '', 0, 120),
              "StrasseHausNr": $substring($a.streetAndNumber ?: '', 0, 35),
              "Ortsteil": $substring($a.district ?: '', 0, 35),
              "LandCode": $a.countryCode,
              "PLZ-S": $a.postalCode,
              "Ort-S": $a.city
            }
          }) ~> $append($count($v.addresses ?: []) < 2 ? {
              "AdressTyp": "CZ",  /** CZ olacak default */
              "AdressCode": "1", /** default 1 */
              "TeilnehmerEORI": "", /** boş kalacak */
              "TeilnehmerNLNR": "", /** boş kalacak */
              "NameFirma": $substring($globalExporterName ?: '', 0, 120), /** sender(consignor) invoice, ihracat beyanname */
              "StrasseHausNr": $substring($globalExporterAddress ?: '', 0, 35), /** default Istanbul */
              "Ortsteil": "", /** default boş */
              "LandCode": $globalExporterCountry, /** supplier_country (TR), invoice, beyanname */
              "PLZ-S": "34000", /** default 34000 */
              "Ort-S": $globalExporterCity /** adres buraya GELECEK, /* invoice, beyanname */
          } : [])
        },
        "WarenPosition": $getWarenPosition($v.headerData.procedureCodeRequested)
      }
    })
  }
}
)